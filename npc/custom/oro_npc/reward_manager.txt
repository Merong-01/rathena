//======== Costum Reward Manager =============================
//============================================================
prontera,147,174,5	script	Reward Manager	4_DR_PEPE,{
function mainRewardManager;
function processSGB;
function addSGBRecipient;
function viewSGBRecipient;
function deleteSGBRecipient;
function processDiscordRew;
function addDiscordRec;
function viewDiscordRec;
function deleteDiscordRec;
function processGengWar;
function addGengWarWinner;
function addGengWarLoser;
function viewGengWar;

	//process start here when npc is clicked
	mes "[ Reward Manager ]";
	mes "Here you can claim rewards from the events you had participated.";
	mes "You can also redeem ";
	mes mesitemlink(30008) + " from here.";
	next;
	
	.@sel = select("Redeem Streamer Gift Box:Discord Membership Reward:GengWar Prize:Cancel");
	if(.@sel == 4){
		mes "[ Reward Manager ]";
		mes "Thanks for visiting.";
		close;
	}
	else if(.@sel == 3){
		if(#GengWarWin == 0){
			mes "[ Reward Manager ]";
			mes "You don't have unclaimed GengWar Prize";
		}
		else {
			.@item_count = 4 * #GengWarWin;
			if(checkweight(12411,.@item_count,12210,.@item_count,12211,.@item_count,12221,.@item_count) == 0){
				mes "[ Reward Manager ]";
				mes "Please make sure your not overweight first before you claim you rewards.";
				close;
			}
			getitem 12411,.@item_count;
			getitem 12210,.@item_count;
			getitem 12211,.@item_count;
			getitem 12221,.@item_count;
			#GengWarWin = 0;
			mes "[ Reward Manager ]";
			mes "Here are your prizes.";
		}
		
		if(#GengWarLost == 0){
			close;
		}
		else {
			next;
			.@item_count = 2 * #GengWarLost;
			if(checkweight(12411,.@item_count,12210,.@item_count,12211,.@item_count,12221,.@item_count) == 0){
				mes "[ Reward Manager ]";
				mes "Please make sure your not overweight first before you claim you items.";
				close;
			}
			getitem 12411,.@item_count;
			getitem 12210,.@item_count;
			getitem 12211,.@item_count;
			getitem 12221,.@item_count;
			#GengWarLost = 0;
			mes "[ Reward Manager ]";
			mes "Here are your consolation prizes.";
			close;
		}
	}
	else if(.@sel == 2){
		if(#discord_register == 0){
			mes "[ Reward Manager ]";
			mes "You don't have unclaimed discord related reward.";
			close;
		}
		if(checkweight(30110,1) == 0){
			mes "[ Reward Manager ]";
			mes "Please make sure your not overweight first before you claim you rewards.";
			close;
		}
		getitem 30110,1;
		#discord_register = 0;
		mes "[ Reward Manager ]";
		mes "Here is your";
		mes mesitemlink(30110);
		mes "Thank you for visiting.";
		close;
	}
	
	.@acc_id = convertpcinfo(strcharinfo(0),CPC_ACCOUNT);
	if(getd("$SGB" + .@acc_id) > 0){
		if(checkweight(30008, getd("$SGB" + .@acc_id)) == 0){
			mes "[ Reward Manager ]";
			mes "Please make sure your not overweight first before you claim you rewards.";
			close;
		}
		
		getitem 30008, getd("$SGB" + .@acc_id);
		setd "$SGB" + .@acc_id, 0;
		$SGBrewardlist_count -= 1;
		
		mes "[ Reward Manager ]";
		mes "Here are your " + mesitemlink(30008);
		mes "Thank you for visiting.";
		close;
	}
	
	mes "[ Reward Manager ]";
	mes "I am sorry, but you don't have unclaimed ";
	mes mesitemlink(30008) + " in this account.";
	close;


	function mainRewardManager {
		mes "Which type of reward you want to view?";
		next;
		
		.@sel = select("Streamer Gift Box:Discord Membership Reward:GengWar Prize:Cancel");
		switch(.@sel){
			case 1: .@ret = processSGB();
				break;
			case 2: .@ret = processDiscordRew();
				break;
			case 3: .@ret = processGengWar();
				break;
			case 4: 
				mes "Reward Manager is closing.";
				close;
				break;
			default:
				break;
		}
		
		if(.@ret == -1){
			mainRewardManager;
		}
		
		return 0;
	}
	
	function processSGB {
		.@rewardname$ = "[Streamer Gift Box]";
		
		do {
			mes .@rewardname$;
			mes "There are currently " + $SGBrewardlist_count + " unclaimed rewards.";
			next;
			
			.@sel = prompt("Add Reward Recipient:View Unclaimed Recipient:Delete Reward Recipient:Cancel");
			if(.@sel == 255){
				return 0;
			}
			switch(.@sel){
				case 1: .@ret = addSGBRecipient();
					break;
				case 2: .@ret = viewSGBRecipient();
					break;
				case 3: .@ret = deleteSGBRecipient();
					break;
				case 4: return -1;
					break;
				default:
					mes "Invalid selection";
					.@ret = -1;
					break;
			}
			clear;
		}while(.@ret == 0);
		
		return 0;
	}
	
	function addSGBRecipient {
		.@rewardname$ = "[Streamer Gift Box]";
		
		mes .@rewardname$;
		mes "Chose the input type.";
		next;
		
		if(select("Username:Player Name") == 2){
			mes .@rewardname$;
			mes "Enter the player name of the recipient:";
			input .@charname$;
			
			.@ret = query_sql("select account_id from ragnaserver.char where (name = '" + .@charname$ + "')", .@account_id);
		}
		else {
			mes .@rewardname$;
			mes "Enter the username of the recipient:";
			input .@username$;
		
			.@ret = query_sql("select account_id from login where (userid = '" + .@username$ + "')", .@account_id);
		}
		
		if(.@ret == 1){
			clear;
			//mes .@rewardname$;
			mes "How many " + mesitemlink(30008) + " will you reward to this account?";
			mes "(Enter 0 to cancel)";
			input .@item_num;
			
			if(.@item_num == 0){
				clear;
				mes .@rewardname$;
				mes "No item was added as a reward to the recipient.";
				next;
				return 0;
			}
			if(getd("$SGB" + .@account_id[.@i]) == 0){
				$SGBrewardlist_count += 1;
			}
			
			setd "$SGB" + .@account_id[0], getd("$SGB" + .@account_id[0]) + .@item_num;
			
			clear;
			mes .@rewardname$;
			mes "Successfully added " + .@item_num + " ";
			mes mesitemlink(30008) + " to recipient.";
			next;
		}
		else {
			mes "Invalid username/player name";
			next;
		}
		return 0;
	}
	
	function viewSGBRecipient {
		.@rewardname$ = "[Streamer Gift Box]";
		
		.@ret = query_sql("select account_id, userid from login", .@account_id, .@username$);
		if(.@ret == 0){
			mes .@rewardname$;
			mes "Failed to retrieve fromm DB.";
			next;
			return 0;
		}
		
		mes .@rewardname$;
		for(.@i = 0; .@i < .@ret; .@i++){
			if(getd("$SGB" + .@account_id[.@i]) == 0){
				continue;
			}
			mes "~[ " + .@username$[.@i] + " ] ------ " + getd("$SGB" + .@account_id[.@i]);
			.@recipient_count += 1;
		}
		
		if($SGBrewardlist_count != .@recipient_count){
			mes "Warning: local count is " + .@recipient_count + " while global count is " + $SGBrewardlist_count;
			$SGBrewardlist_count = .@recipient_count;
		}
		next;
		
		return 0;
	}
	
	function deleteSGBRecipient {
		.@rewardname$ = "[Streamer Gift Box]";
		
		mes .@rewardname$;
		mes "Enter the username of the recipient you want to delete:";
		input .@username$;
		
		.@ret = query_sql("select account_id from login where (userid = '" + .@username$ + "')", .@account_id);
		
		if(.@ret == 1){
			if(getd("$SGB" + .@account_id[0]) == 0){
				clear;
				mes .@rewardname$;
				mes "Recipient has no unclaimed " + mesitemlink(30008);
				next;
				return 0;
			}
			
			setd "$SGB" + .@account_id[0], 0;
			$SGBrewardlist_count -= 1;
			clear;
			mes .@rewardname$;
			mes "Successfully deleted unclaimed " + mesitemlink(30008) + " from recipient.";
			next;
		}
		else {
			mes "Invalid username";
			next;
		}
		
		return 0;
	}
	
	function processDiscordRew {
		.@rewardname$ = "[ Discord Reward ]";
		do {
			mes .@rewardname$;
			mes "Select the operation you want to execute";
			next;
			
			switch(select("Add Recipient:View Recipients:Delete Recipient:Cancel")){
				case 1: .@ret = addDiscordRec();
					break;
				case 2: .@ret = viewDiscordRec();
					break;
				case 3: .@ret = deleteDiscordRec();
					break;
				default: return -1;
					break;
			}
			clear;
		}while(.@ret == 0);
		
		return 0;
	}
	
	function addDiscordRec {
		.@rewardname$ = "[ Discord Reward ]";
		
		mes .@rewardname$;
		mes "Enter the player's name:";
		input .@charname$;
		
		.@ret = query_sql("select account_id from ragnaserver.char where (name = '" + .@charname$ + "')", .@account_id);
		if(.@ret == 0){
			mes .@rewardname$;
			mes "Player Name," + .@charname$ + ", does not exist";
			next;
			
			return 0;
		}
		
		clear;
		mes .@rewardname$;
		mes "Reward Item List";
		next;
		
		switch(select("Costume Bubblegum-in-Mouth:Cancel")){
			case 1: //#discord_register = 1;
				.@ret = query_sql("REPLACE INTO acc_reg_num VALUES (" + .@account_id[0] + ",'#discord_register', 0, 1)");
				if(.@ret == -1){
					mes "Failed to add in the database!";
					next;
					
					return 0;
				}
				
				mes .@rewardname$;
				mes "Successfully added player as recipient";
				next;
				break;
			default:
				break;
		}
		
		return 0;
	}
	
	function viewDiscordRec {
		.@rewardname$ = "[ Discord Reward ]";
		
		.@ret = query_sql("select account_id from acc_reg_num where (acc_reg_num.key = '#discord_register') and (acc_reg_num.value = 1)", .@account_id);
		
		if(.@ret == 0){
			mes .@rewardname$;
			mes "There are no unclaimed/recorded recipient.";
			next;
			
			return 0;
		}
		
		mes .@rewardname$;
		for(.@i = 0; .@i < .@ret; .@i++){
			.@num = query_sql("select userid from login where (account_id = " + .@account_id[.@i] + ")", .@username$);
			if(.@num == 1){
				mes "~[ " + .@username$[.@i] + " ] ------ 1";
			}
			if(.@i % 10 == 9){
				next;
				mes .@rewardname$;
			}
		}
		next;
		
		return 0;
	}
	
	function deleteDiscordRec {
		.@rewardname$ = "[ Discord Reward ]";
		
		mes .@rewardname$;
		mes "Enter the username of the recipient you want to delete:";
		input .@username$;
		
		.@ret = query_sql("select account_id from login where (userid = '" + .@username$ + "')", .@account_id);
		if(.@ret == 1){
			.@num = query_sql("UPDATE acc_reg_num SET acc_reg_num.value = 0 where acc_reg_num.account_id = " + .@account_id[0] + " and acc_reg_num.key = '#discord_register' and acc_reg_num.index = 0");
		
			if(.@num == -1){
				mes "Failed to delete record";
				next;
				
				return 0;
			}
			clear;
			mes .@rewardname$;
			mes "Successfully deleted recipient.";
			next;
		}
		else {
			mes .@rewardname$;
			mes "Invalid username";
			next;
		}
		
		return 0;
	}
	
	function processGengWar {
		.@rewardname$ = "[ GengWar ]";
		
		do {
			mes .@rewardname$;
			mes "Select the operation you want to perform:";
			next;
			
			switch(select("Add GengWar Winner:Add GengWar Loser:View GengWar Record:Cancel")){
				case 1: addGengWarWinner;
					break;
				case 2: addGengWarLoser;
					break;
				case 3: viewGengWar;
					break;
				default: return -1;
					break;
			}
			clear;
		}while(.@ret == 0);
		
		return 0;
	}
	
	function addGengWarWinner {
		.@rewardname$ = "[ GengWar ]";
		
		mes .@rewardname$;
		mes "Enter the player name of the GengWar winner:";
		input .@charname$;
		
		.@ret = query_sql("select account_id from ragnaserver.char where (name = '" + .@charname$ + "')", .@account_id);
		if(.@ret == 0){
			mes .@rewardname$;
			mes "Player Name," + .@charname$ + ", does not exist";
			next;
			
			return 0;
		}
		clear;
		.@ret = query_sql("select acc_reg_num.value from acc_reg_num where acc_reg_num.account_id = '" + .@account_id[0] + "' and acc_reg_num.key = '#GengWarWin' and acc_reg_num.index = 0", .@value);
		.@value[0] += 1;
		.@ret = query_sql("REPLACE INTO acc_reg_num VALUES (" + .@account_id[0] + ",'#GengWarWin', 0, " + .@value[0] +")");
		
		if(.@ret == -1){
			mes "Failed to add GengWar winner!";
			next;
			return 0;
		}
		
		mes .@rewardname$;
		mes "Successfully added player as GengWar winner.";
		next;
		
		return 0;
	}
	
	function addGengWarLoser {
		.@rewardname$ = "[ GengWar ]";
		
		mes .@rewardname$;
		mes "Enter the player name of the GengWar loser:";
		input .@charname$;
		
		.@ret = query_sql("select account_id from ragnaserver.char where (name = '" + .@charname$ + "')", .@account_id);
		if(.@ret == 0){
			mes .@rewardname$;
			mes "Player Name," + .@charname$ + ", does not exist";
			next;
			
			return 0;
		}
		clear;
		.@ret = query_sql("select acc_reg_num.value from acc_reg_num where acc_reg_num.account_id = '" + .@account_id[0] + "' and acc_reg_num.key = '#GengWarLost' and acc_reg_num.index = 0", .@value);
		.@value[0] += 1;
		.@ret = query_sql("REPLACE INTO acc_reg_num VALUES (" + .@account_id[0] + ",'#GengWarLost', 0, " + .@value[0] +")");
		
		if(.@ret == -1){
			mes "Failed to add GengWar loser!";
			next;
			return 0;
		}
		
		mes .@rewardname$;
		mes "Successfully added player as GengWar loser.";
		next;
		
		return 0;
	}
	
	function viewGengWar {
		.@rewardname$ = "[ GengWar ]";
		
		.@ret = query_sql("select acc_reg_num.account_id, acc_reg_num.value from acc_reg_num where acc_reg_num.key = '#GengWarWin' and acc_reg_num.index = 0 and acc_reg_num.value > 0", .@account_id, .@value);
		if(.@ret < 1){
			mes .@rewardname$;
			mes "The are no unclaimed/recorded GengWar winners";
			//next;
		}
		else {
			mes .@rewardname$;
			mes "GengWar Winners:";
			for(.@i = 0; .@i < .@ret; .@i++){
				.@num = query_sql("select userid from login where (account_id = " + .@account_id[.@i] + ")", .@username$);
				if(.@num == 1){
					mes "~[ " + .@username$[.@i] + " ] ------ " + .@value[.@i];
				}
				if(.@i % 10 == 9){
					next;
					mes .@rewardname$;
					mes "GengWar Winners:";
				}
			}
		}
		next;
		
		.@ret = query_sql("select acc_reg_num.account_id, acc_reg_num.value from acc_reg_num where acc_reg_num.key = '#GengWarLost' and acc_reg_num.index = 0 and acc_reg_num.value > 0", .@account_id2, .@value2);
		if(.@ret < 1){
			mes .@rewardname$;
			mes "The are no unclaimed/recorded GengWar losers";
			next;
			return 0;
		}
		else {
			mes .@rewardname$;
			mes "GengWar Losers:";
			for(.@i = 0; .@i < .@ret; .@i++){
				.@num = query_sql("select userid from login where (account_id = " + .@account_id2[.@i] + ")", .@username2$);
				if(.@num == 1){
					mes "~[ " + .@username2$[.@i] + " ] ------ " + .@value2[.@i];
				}
				if(.@i % 10 == 9){
					next;
					mes .@rewardname$;
					mes "GengWar Losers:";
				}
			}
		}
		next;
		
		return 0;
	}

OnInit:
	bindatcmd("rewardmanager",strnpcinfo(0)+"::OnCommand",99,99);
	end;
OnCommand:
	mainRewardManager;
	end;
}
