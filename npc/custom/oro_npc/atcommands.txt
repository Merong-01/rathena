//======== Custom Atcommands =================================
//============================================================
-	script	custom atcommands	-1,{
end;
OnInit:
	bindatcmd("shout",strnpcinfo(0)+"::OnCommand",0,99);
	bindatcmd("vipstatus",strnpcinfo(0)+"::OnVIPStatus",0,99);
	bindatcmd("mapshower",strnpcinfo(0)+"::OnMapShower",10,99);
end;
OnMapShower:
	.@npcLable$ = "[Map Shower Command]";
	.@current_map$ = strcharinfo(3);
	
	mes .@npcLable$;
	mes "This operation will give items directly to ALL players in the SAME map you are now.";
	next;
	
	mes .@npcLable$;
	mes "Carefully select which item you want to give.";
	next;
	
	.@item_id[0] = 14003; // ESSB
	.@item_id[1] = 30001; // ESSB (pvp/gvg)
	.@item_id[2] = 12411; // HE Battle Manual
	.@item_id[3] = 14592; // Job Manual50
	.@item_id[4] = 12210; // Bubble Gum
	.@item_id[5] = 12211; // Kafra Card
	.@item_id[6] = 12221; // Megaphone
	.@item_id[7] = 30005; // 1Day VIP
	.@item_id[8] = 30007; // Egg Scrool
	.@item_id[9] = 12911; // neuralizer box
	.@item_id[10] = 6320; // reset stone
	.@item_id[11] = 30010; // new hair dye coupon
	.@item_id[12] = 30126; // costume teddybear hat
	
	.@item_name_sel$ = "";
	for(.@i = 0; .@i < getarraysize(.@item_id); .@i++){
		.@item_name_sel$ += getitemname(.@item_id[.@i]) + ":";
	}
	.@item_name_sel$ += "Cancel";
	
	.@sel = select(.@item_name_sel$) - 1;
	if(.@sel >= getarraysize(.@item_id) || .@sel < 0){
		mes "Map Shower Command is Closing...";
		close;
	}
	
	mes .@npcLable$;
	mes "You selected " + mesitemlink(.@item_id[.@sel]);
	mes "Select the number of items you want to give:";
	next;
	
	if(.@item_id[.@sel] == 30007 || .@item_id[.@sel] == 12911 || .@item_id[.@sel] == 6320 || .@item_id[.@sel] == 30126){
		.@num = select("1");
	}
	else {
		.@num = select("1:2:3:4:5:10");
	}
	
	if(.@num > 5){
		.@num = 10;
	}
	
	mes .@npcLable$;
	mes "Are you sure you want to give " + mesitemlink(.@item_id[.@sel]) + " " + .@num + " pcs ea.";
	mes "to ALL players of map " + .@current_map$ + "?";
	next;
	select("Yes, I am sure!:Cancel");
	if(@menu == 2){
		mes "Map Shower Command is Closing...";
		close;
	}
	
	.@index = getarraysize(@item_to_give);
	@item_to_give[.@index] = .@item_id[.@sel];
	@item_to_give_num[.@index] = .@num;
	
	addtimer 29000, strnpcinfo(0) + "::OnExecuteShower";
	showdigit 30, 2;
	
	mes .@npcLable$;
	mes .@num + " pcs of " + mesitemlink(.@item_id[.@sel]) + " will be given to ALL players ";
	mes "of map " + .@current_map$ + " after the countdown ends.";
	mes "If you change your mind and want to cancel the operation, just logout before the countdown ends.";
	mes "^FF0000(To avoid errors, close this window before the countdown ends.)^000000";
	close2;
end;
OnExecuteShower:
	.@current_map$ = strcharinfo(3);
	.@player_num = getmapunits(BL_PC, .@current_map$, .@user_id[0]);
	
	freeloop(1);
	
	for(.@i = 0; .@i < .@player_num; .@i++){
		getitembound @item_to_give[0], @item_to_give_num[0], Bound_Account, .@user_id[.@i];
	}
	deletearray @item_to_give[0], 1;
	deletearray @item_to_give_num[0], 1;
	if(getarraysize(@item_to_give) == 0){
		showdigit 0;
	}
	freeloop(0);
end;
OnCommand:
	if(@shout_cd){
		dispbottom "Shout is still in cool down", 0xFF0000;
		end;
	}
	if(.@atcmd_numparameters == 0){
		dispbottom "Invalid Parameter", 0x00FF00;
		dispbottom "@shout <message here>", 0x00FF00;
		end;
	}
	announce strcharinfo(0) + ": " + implode(.@atcmd_parameters$, " "), bc_all, 0x00FF99, FW_BOLD, 14;
	//atcommand "@broadcast " + implode(.@atcmd_parameters$, " ");
	@shout_cd = 1;
	addtimer 120000,strnpcinfo(0) + "::OnCoolDown";
end;
OnCoolDown:
	@shout_cd = 0;
end;
OnVIPStatus:
	if(vip_status(VIP_STATUS_ACTIVE) == false){
		dispbottom "Your VIP Status is currenlty not active.", 0x00FF00;
		end;
	}

	dispbottom "Your VIP Status is active!", 0x00FF00;
	dispbottom "Your remaining VIP time is " + F_gettimestr(vip_status(VIP_STATUS_REMAINING)), 0x00FF00;
end;
}

-	script	Streamer Code	-1,{
end;
OnInit:
	bindatcmd("streamerredeem",strnpcinfo(0)+"::OnRedeem",99,99);
	bindatcmd("setstreamercode",strnpcinfo(0)+"::OnSetSCode",99,99);
	bindatcmd("streamerpoints",strnpcinfo(0)+"::OnStreamerPoints",1,99);
end;
OnSetSCode:
	if(.@atcmd_numparameters < 1){
		dispbottom "Invalid Parameter", 0x00FF00;
		dispbottom "@setstreamercode <streamer code>", 0x00FF00;
		end;
	}
	
	.@streamercode$ = .@atcmd_parameters$[0];
	
	if(getstrlen(.@streamercode$) > 12){
		dispbottom "Streamer Code exceeded the 12 character limit!", 0xFF0000;
		end;
	}
	
	setd "$" + .@streamercode$, 1;
	$StreamerCodeCount += 1;
	
	dispbottom "Streamer Code " + .@streamercode$ + " successfully registered.", 0x00FF00;
end;
OnStreamerPoints:
	if(.@atcmd_numparameters < 1){
		dispbottom "Invalid Parameter", 0x00FF00;
		dispbottom "@streamerpoints <streamer code>", 0x00FF00;
		end;
	}
	.@streamercode$ = .@atcmd_parameters$[0];
	
	if(getd("$" + .@streamercode$) == 1){
		dispbottom .@streamercode$ + " current points: " + getd("$SCP_" + .@streamercode$), 0x00FF00;
		dispbottom "Bonus (common) points: " + $StreamerAll_points, 0x00FF00;
	}
end;
OnRedeem:
	.@npcName$ = "[Redeem Streamer Points]";
	
	mes .@npcName$;
	mes "Enter the streamer code of the streamer to redeem:";
	input .@streamercode$;
	
	clear;
	if(strtolower(.@streamercode$) == "bonus"){
		mes .@npcLable$;
		mes "Current Bonus (common) points: " + $StreamerAll_points;
		mes "Enter amount to redeem: ";
		input .@amount;
		clear;
		if(.@amount == 0){
			mes .@npcName$;
			mes "Invalid amount!";
			close;
		}
		if($StreamerAll_points < .@amount){
			mes .@npcName$;
			mes "Amount entered is greater than available points.";
			close;
		}
		$StreamerAll_points -= .@amount;
		mes .@npcLable$;
		mes "Remaining Bonus (common) points is " + $StreamerAll_points;
		close;
	}
	if(getd("$" + .@streamercode$) == 0){
		mes .@npcName$;
		mes "Invalid Streamer Code!";
		close;
	}
	mes .@npcName$;
	mes .@streamercode$ + " current points: " + getd("$SCP_" + .@streamercode$);
	mes "Enter the amount to redeem:";
	mes "^FF0000Note: this amount will deducted from the points of the streamer^000000";
	input .@amount;
	
	clear;
	if(.@amount == 0){
		mes .@npcName$;
		mes "Invalid amount!";
		close;
	}
	if(getd("$SCP_" + .@streamercode$) < .@amount){
		mes .@npcName$;
		mes "Amount entered is greater than available points.";
		close;
	}
	setd "$SCP_" + .@streamercode$, getd("$SCP_" + .@streamercode$) - .@amount;
	mes .@npcName$;
	mes .@streamercode$ + " remaining points: " + getd("$SCP_" + .@streamercode$);
end;
}

-	script	PvP Battle	-1,{
OnInit:
	bindatcmd("battlestart", strnpcinfo(0) + "::OnBattleStart", 10, 99);
end;
OnBattleStart:
	.pvp_flag = getmapflag(strcharinfo(3), mf_pvp);
	.itemuse_flag = getmapflag(strcharinfo(3), mf_noitemconsumption);
	unbindatcmd "battlestart";
	.timer_end = 0;
	bindatcmd("battleend", strnpcinfo(0) + "::OnBattleEnd", 10, 99);
	callsub StartCountdown, "Battle will start in", 5;
	announce "Start!!!", bc_all, 0x00FF00, FW_BOLD, 35;
	setmapflag strcharinfo(3), mf_pvp;
	
	attachnpctimer;
	initnpctimer;
end;
OnTimer180000:
	announce "Three minutes has passed since the battle started.", bc_all, 0x00FF00, FW_BOLD, 20;
end;
OnTimer181000:
	callsub StartCountdown, "Disabling item use/consumption in", 10;
	setmapflag strcharinfo(3), mf_noitemconsumption;
	announce "Item use/consumption is now disabled!", bc_all, 0x00FF00, FW_BOLD, 20;
end;
//OnTimer25000:
OnTimer600000:
OnBattleEnd:
	.timer_end = 1;
	awake strnpcinfo(0);
	removemapflag strcharinfo(3), mf_noitemconsumption;
	removemapflag strcharinfo(3), mf_pvp;

	unbindatcmd "battleend";
	bindatcmd("battlestart", strnpcinfo(0) + "::OnBattleStart", 10, 99);
	announce "Battle Ended!", bc_all, 0x00FF00, FW_BOLD, 20;
	if(.pvp_flag){
		setmapflag strcharinfo(3), mf_pvp;
	}
	if(.itemuse_flag){
		setmapflag strcharinfo(3), mf_noitemconsumption;
	}
	stopnpctimer;
	detachnpctimer;
end;
StartCountdown:
	for(.@num = getarg(1); .@num > 0; .@num--){
		if(.timer_end == 1){
			end;
		}
		announce getarg(0) + " " + .@num + "...", bc_all, 0x00FF00, FW_BOLD, 20;
		sleep2 1000;
	}
	if(.timer_end == 1){
		end;
	}
return;
}

-	script	Restrict Unfrozen	-1,{
OnInit:
	bindatcmd("evildruidoff", strnpcinfo(0) + "::OnEvilDruidOff", 10, 99);
	bindatcmd("evildruidon", strnpcinfo(0) + "::OnEvilDruidOn", 10, 99);
	bindatcmd("marcevildruidoff", strnpcinfo(0) + "::OnMarcEvilDruidOff", 10, 99);
	bindatcmd("marcevildruidon", strnpcinfo(0) + "::OnMarcEvilDruidOn", 10, 99);
end;
OnEvilDruidOff:
	//if(getmapflag(strcharinfo(3), mf_restricted) == 11){
	//	dispbottom "@evildruidoff failed...", 0x00FF00;
	//	dispbottom "Evil Druid Card is already disabled on this map.", 0x00FF00;
	//	end;
	//}
	setmapflag strcharinfo(3), mf_restricted, 11;
	dispbottom "Evil Druid Card is now disabled on this map.", 0x00FF00;
end;
OnEvilDruidOn:
	removemapflag strcharinfo(3), mf_restricted, 11;
	dispbottom "Evil Druid Card is now enabled on this map.", 0x00FF00;
end;
OnMarcEvilDruidOff:
	setmapflag strcharinfo(3), mf_restricted, 10;
	dispbottom "Marc and Evil Druid Card is now disabled on this map.", 0x00FF00;
end;
OnMarcEvilDruidOn:
	removemapflag strcharinfo(3), mf_restricted, 10;
	dispbottom "Marc and Evil Druid Card is now enabled on this map.", 0x00FF00;
end;
}
