//====== Specialized Costume Enchanter NPC ===================
//============================================================
prontera,167,200,5	script	Costume Enchanter	4_F_CACTUSLADY3,{
	//.@npcName$ = "[ Costume Enchanter ]";
	
	mes "[ Costume Enchanter ]";
	mes "I enchant costume equipments to have additional status effects.";
	mes "For now, I can add ^0000FF+Weight^000000 capacity enchantment,";
	mes "^0000FFteleport^000000 skill enchantment and ^0000FF+MoveSpeed^000000 enchantment.";
	next;
	
	select("Enchant Costume - Weight:Enchant Costume - Teleport:Enchant Costume - MoveSpeed:Cancel");
	if(@menu == 1){
		callsub S_Enchant_Weight;
	}
	else if(@menu == 2){
		callsub S_Enchant_Teleport;
	}
	else if(@menu == 3){
		callsub S_Enchant_MoveSpeed;
	}
	else {
		mes "[ Costume Enchanter ]";
		mes "Come again if you have something to enchant.";
		close;
	}
end;
S_Enchant_Weight:
	.@npcName$ = "[ Costume Enchanter ]";

	mes .@npcName$;
	mes "Currently, I can only do Weight enchantment on ^0000FF Relic Costumes^000000";
	mes "Just bring to me the item and a ^0000FF" + getitemname(7776) + "^000000";
	mes "For every ^0000FF" + getitemname(7776) + 
		"^000000, I can enchant ^0000FF+100 Weight^000000 capacity";
	next;
	
	mes .@npcName$;
	mes "This enchantment is a little special where" + 
		" you can reenchant the equipment to improve the status effect up to " + 
		"^0000FF+1000 Weight^000000 capacity";
	mes "Keep in mind that enchantments other than " + 
		"^0000FF+Weight^000000 will disappear after the enchantment";
	next;
	
	mes .@npcName$;
	mes "Do you have the costume and the required materials";
	mes "so we can do some enchanting?";
	next;
	
	if(select("Sorry, maybe next time.:Yes, let's proceed.") == 1) {
		mes .@npcName$;
		mes "Please come back when you have any interest in enchanting your costume.";
		close;
	}
	
	setarray .@enchantable_itemid[0], 30240, 30241, 30242, 30243, 30244, 30245; // 2-1 jobs relic garment
	setarray .@enchantable_itemid[6], 30246, 30247, 30248, 30249, 30250, 30251, 30252; // 22 jobs relic garment
	setarray .@enchantable_itemid[13], 30253, 30254, 30255, 30256, 30257; // extended jobs relic garment
	
	.@sel_itemname$ = "";
	for(.@i = 0; .@i < getarraysize(.@enchantable_itemid); .@i++){
		.@sel_itemname$ += getitemname(.@enchantable_itemid[.@i]) + ":";
	}
	.@sel_itemname$ += "Cancel";
	
	mes .@npcName$;
	mes "Which costume you want to enchant?";
	next;
	
	.@sel_item = select(.@sel_itemname$) - 1;
	
	if(.@sel_item >= getarraysize(.@enchantable_itemid)){
		mes .@npcName$;
		mes "Talk to me again if you want to enchant your costume.";
		close;
	}
	
	if(countitem(.@enchantable_itemid[.@sel_item]) < 1){
		mes .@npcName$;
		mes "Sorry but I can't see the " + getitemname(.@enchantable_itemid[.@sel_item]) + " in your inventory.";
		close;
	}
	else if(countitem(7776) < 1){
		mes .@npcName$;
		mes "Sorry but you don't have the " + getitemname(7776) + " to enchant your costume.";
		close;
	}
	else if(checkweight(.@enchantable_itemid[.@sel_item], 1) == 0){
		mes .@npcName$;
		mes "Sorry but please make sure you are not overweight before doing an enchantment.";
		close;
	}
	
	mes .@npcName$;
	mes "Another important reminder,";
	mes "if there are two costumes of the same kind in your" + 
		" possession, the Enchantment will be applied in the " + 
		"order they are placed in your inventory.";
	next;
	
	mes .@npcName$;
	mes "In that case, the Enchantment may" + 
		" be applied to an item which you didn't " + 
		"mean to Enchant. So just bring " + 
		"^5555ffONE Costume^000000 you want enchanted to be safe...";
	next;
	
	mes .@npcName$;
	mes "I'm not responsible for what " + 
		"would happen if you have more " + 
		"than one of the same kind in your inventory.";
	next;
	
	if(select("Wait...let me check my inventory:Go ahead...") == 1){
		mes .@npcName$;
		mes "Please come back after you sort out your inventory.";
		close;
	}
	
	mes .@npcName$;
	mes "Good! Now, let me perform the enchantment.";
	close2;
	
	specialeffect2 EF_MAPPILLAR;
	progressbar "FFFF00",5;
	getinventorylist();
	for(.@i = 0; .@i < @inventorylist_count; .@i++){
		if(@inventorylist_id[.@i] == .@enchantable_itemid[.@sel_item]){
			.@currentEnchant = @inventorylist_card4[.@i];
			.@item_index = @inventorylist_idx[.@i];
			.@is_equiped = @inventorylist_equip[.@i];
			break;
		}
	}
	if(.@is_equiped != 0){
		mes .@npcName$;
		mes "Oh! It seems that the fail safe mechanism was triggered.";
		mes "Please unequip the costume to enchant first.";
		mes "You may lost your item if you suddenly become overweight after the enchantment.";
		close;
	}
	if(.@currentEnchant == 30120){
		mes .@npcName$;
		mes "Oh! It seems that this item can't be enchanted anymore.";
		close;
	}
	if(.@currentEnchant < 30111 || .@currentEnchant > 30120){
		.@newEnchant = 30111;
	}
	else {
		.@newEnchant = .@currentEnchant + 1;
	}
	if(countitem(.@enchantable_itemid[.@sel_item]) < 1 || 
		countitem(7776) < 1) // @inventorylist_id[.@item_index] != .@enchantable_itemid[.@sel_item]
	{
		mes .@npcName$;
		mes "Where are the items?";
		mes "Did you manage to hide it without me noticing?";
		close;
	}
	specialeffect2 EF_LORD;
	delitemidx .@item_index, 1;
	delitem 7776, 1;
	getitem2 .@enchantable_itemid[.@sel_item], 1, 1, 0, 0, 0, 0, 0, .@newEnchant;
	
	mes .@npcName$;
	mes "There you go.";
	mes "In the future, I should be able to enchant more kind of costumes.";
	mes "Feel free to check on me for any update.";
	close2;
end;
S_Enchant_Teleport:
	.@npcName$ = "[ Costume Enchanter ]";

	mes .@npcName$;
	mes "Currently, I can only do Teleport enchantment on ^0000FF Costume Vicious Aura Mid^000000";
	mes "Just bring to me the costume and a ^0000FF" + getitemname("Creamy_Card") + "^000000";
	mes "so that I can enchant it with the teleport skill.";	
	next;

	mes .@npcName$;
	mes "This enchantment looks simple but very effective.";
	mes "It will save you a accessory slot intended for creamy card that you can use to slot other useful cards.";
	mes "Keep in mind that enchantments other than " + 
		"^0000FFTeleport^000000 will disappear after the enchantment";
	next;
	
	mes .@npcName$;
	mes "Do you have the costume and the required materials";
	mes "so we can do some enchanting?";
	next;

	if(select("Sorry, maybe next time.:Yes, let's proceed.") == 1) {
		mes .@npcName$;
		mes "Please come back when you have any interest in enchanting your costume.";
		close;
	}
	if(countitem(31236) < 1){
		mes .@npcName$;
		mes "Sorry but I can't see the " + getitemname(31236) + " in your inventory.";
		close;
	}
	else if(countitem(4040) < 1){
		mes .@npcName$;
		mes "Sorry but you don't have the " + getitemname(4040) + " to enchant your costume.";
		close;
	}
	else if(checkweight(31236, 1) == 0){
		mes .@npcName$;
		mes "Sorry but please make sure you are not overweight before doing an enchantment.";
		close;
	}
	
	mes .@npcName$;
	mes "Another important reminder,";
	mes "if there are two costumes of the same kind in your" + 
		" possession, the Enchantment will be applied in the " + 
		"order they are placed in your inventory.";
	next;
	
	mes .@npcName$;
	mes "In that case, the Enchantment may" + 
		" be applied to an item which you didn't " + 
		"mean to Enchant. So just bring " + 
		"^5555ffONE Costume^000000 you want enchanted to be safe...";
	next;
	
	mes .@npcName$;
	mes "I'm not responsible for what " + 
		"would happen if you have more " + 
		"than one of the same kind in your inventory.";
	next;
	
	if(select("Wait...let me check my inventory:Go ahead...") == 1){
		mes .@npcName$;
		mes "Please come back after you sort out your inventory.";
		close;
	}
	mes .@npcName$;
	mes "Good! Now, let me perform the enchantment.";
	close2;
	
	specialeffect2 EF_MAPPILLAR;
	progressbar "FFFF00",5;
	getinventorylist();
	for(.@i = 0; .@i < @inventorylist_count; .@i++){
		if(@inventorylist_id[.@i] == 31236){
			.@currentEnchant = @inventorylist_card4[.@i];
			.@item_index = @inventorylist_idx[.@i];
			break;
		}
	}
	if(.@currentEnchant == 4040){
		mes .@npcName$;
		mes "Oh! It seems that this costume already has a teleport enchantment.";
		close;
	}
	else {
		.@newEnchant = 4040;
	}
	if(countitem(31236) < 1 || 
		countitem(4040) < 1) // @inventorylist_id[.@item_index] != 31236
	{
		mes .@npcName$;
		mes "Where are the items?";
		mes "Did you manage to hide it without me noticing?";
		close;
	}
	specialeffect2 EF_LORD;
	delitemidx .@item_index, 1;
	delitem 4040, 1;
	getitem2 31236, 1, 1, 0, 0, 0, 0, 0, .@newEnchant;
	
	mes .@npcName$;
	mes "There you go.";
	mes "In the future, I should be able to enchant more kind of costumes.";
	mes "Feel free to check on me for any update.";
	close2;
end;
S_Enchant_MoveSpeed:
	.@npcName$ = "[ Costume Enchanter ]";

	mes .@npcName$;
	mes "Currently, I can only do ^0000FF+MoveSpeed^000000 enchantment on ^0000FF Costume Poring Soap Pipe^000000";
	mes "Just bring to me the costume and a ^0000FF" + getitemname("Pecopeco_Hairband") + "^000000";
	mes "so that I can enchant it with ^0000FF+MoveSpeed^000000 enchantment.";
	next;

	mes .@npcName$;
	mes "This enchantment looks simple but very effective.";
	mes "It will save you a equipment slot intended for ^0000FF+MoveSpeed^000000 equipment or cards that  you can use to slot other useful equipment/cards.";
	next;

	mes .@npcName$;
	mes "Keep in mind that enchantments other than " + 
		"^0000FF+MoveSpeed^000000 will disappear after the enchantment";
	next;
	
	mes .@npcName$;
	mes "Do you have the costume and the required materials";
	mes "so we can do some enchanting?";
	next;

	if(select("Sorry, maybe next time.:Yes, let's proceed.") == 1) {
		mes .@npcName$;
		mes "Please come back when you have any interest in enchanting your costume.";
		close;
	}
	if(countitem(31055) < 1){ // C_Poring_Soap_Pipe
		mes .@npcName$;
		mes "Sorry but I can't see the " + getitemname(31055) + " in your inventory.";
		close;
	}
	else if(countitem(5286) < 1){ // Pecopeco_Hairband
		mes .@npcName$;
		mes "Sorry but you don't have the " + getitemname(5286) + " to enchant your costume.";
		close;
	}
	else if(checkweight(31055, 1) == 0){ // C_Poring_Soap_Pipe
		mes .@npcName$;
		mes "Sorry but please make sure you are not overweight before doing an enchantment.";
		close;
	}
	
	mes .@npcName$;
	mes "Another important reminder,";
	mes "if there are two costumes of the same kind in your" + 
		" possession, the Enchantment will be applied in the " + 
		"order they are placed in your inventory.";
	next;
	
	mes .@npcName$;
	mes "In that case, the Enchantment may" + 
		" be applied to an item which you didn't " + 
		"mean to Enchant. So just bring " + 
		"^5555ffONE Costume^000000 you want enchanted to be safe...";
	next;
	
	mes .@npcName$;
	mes "I'm not responsible for what " + 
		"would happen if you have more " + 
		"than one of the same kind in your inventory.";
	next;
	
	if(select("Wait...let me check my inventory:Go ahead...") == 1){
		mes .@npcName$;
		mes "Please come back after you sort out your inventory.";
		close;
	}
	mes .@npcName$;
	mes "Good! Now, let me perform the enchantment.";
	close2;
	
	specialeffect2 EF_MAPPILLAR;
	progressbar "FFFF00",5;
	getinventorylist();
	for(.@i = 0; .@i < @inventorylist_count; .@i++){
		if(@inventorylist_id[.@i] == 31055){
			.@currentEnchant = @inventorylist_card4[.@i];
			.@item_index = @inventorylist_idx[.@i];
			break;
		}
	}
	if(.@currentEnchant == 30127){
		mes .@npcName$;
		mes "Oh! It seems that this costume already has a teleport enchantment.";
		close;
	}
	else {
		.@newEnchant = 30127;
	}
	if(countitem(31055) < 1 || 
		countitem(5286) < 1) // @inventorylist_id[.@item_index] != 31055
	{
		mes .@npcName$;
		mes "Where are the items?";
		mes "Did you manage to hide it without me noticing?";
		close;
	}
	specialeffect2 EF_LORD;
	delitemidx .@item_index, 1;
	delitem 5286, 1;
	getitem2 31055, 1, 1, 0, 0, 0, 0, 0, .@newEnchant;
	
	mes .@npcName$;
	mes "There you go.";
	mes "In the future, I should be able to enchant more kind of costumes.";
	mes "Feel free to check on me for any update.";
	close2;
end;
}
