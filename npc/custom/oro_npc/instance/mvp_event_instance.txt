//======= MVP Boss Random Summon Instance ====================
//============================================================
-	script	MVP Boss Random Instance	-1,{
OnInit:
	bindatcmd("startmvpbosshunt",strnpcinfo(0)+"::OnInitEvent",10,99);
end;
OnClock2120:
OnInitEvent:
	if(.ins_id > 0){
		if(playerattached()){
			dispbottom "An instance of MVP Boss Random Summon Event is already active", 0x00FF00;
		}
		else {
			debugmes "An instance of MVP Boss Random Summon Event is already active";
		}
		end;
	}
	for(.@i = 0; .@i <= getarraysize($TimeEventFlag); .@i++){
		if($TimeEventFlag[.@i] == 0){
			.event_flag_index = .@i;
			break;
		}
	}
	if(.event_flag_index == 0){
		.joinevent_cmdstr$ = "joinevent";
		$TimeEventFlag[0] = 1;
	}
	else {
		.joinevent_cmdstr$ = "joinevent" + (.event_flag_index + 1);
		$TimeEventFlag[.event_flag_index] = 1;
	}
	bindatcmd("joinmvpbosshunt", strnpcinfo(0)+"::OnJoinEvent",0,99);
	bindatcmd(.joinevent_cmdstr$, strnpcinfo(0)+"::OnJoinEvent",0,99);
	.ins_id = instance_create("MVP Night Event", IM_NONE);

	if(.ins_id == -4){
		if(playerattached()){
			dispbottom "No free instance!", 0xFF0000;
		}
		else {
			debugmes "No free instance!";
		}
		end;
	}
	else if(.ins_id < 1){
		if(playerattached()){
			dispbottom "Failed to create instance", 0xFF0000;
		}
		else {
			debugmes "Failed to create instance";
		}
		end;
	}

	cleararray .join_char_id[0], 0, getarraysize(.join_char_id);
	announce "The MVP Boss Random Summon Event will start in 2 minutes, to join the event type @joinmvpbosshunt or @" + .joinevent_cmdstr$, bc_all, 0x00FFFF;
	.mvp_event_status = 1;
	initnpctimer;
end;
OnTimer60000:
	announce "The MVP Boss Random Summon Event will start in 1 minute, to join the event type @joinmvpbosshunt or @" + .joinevent_cmdstr$, bc_all, 0x00FFFF;
end;
OnTimer120000:
	.mvp_event_status = 2;
	announce "A total of " + getarraysize(.join_char_id) + " players have registered to join the event." +
		"The MVP Boss Random Summon Event is Starting, teleporting participants to Event Map instance.", bc_all, 0x00FFFF;
	
	for(.@i = 0; .@i < getarraysize(.join_char_id); .@i++){
		if(isloggedin(convertpcinfo(.join_char_id[.@i], CPC_ACCOUNT), .join_char_id[.@i])){
			.@ret = instance_enter("MVP Night Event", -1, -1, .join_char_id[.@i], .ins_id);
			switch(.@ret){
				case IE_NOMEMBER: .@retStr$ = "No Member";
					break;
				case IE_NOINSTANCE: .@retStr$ = "No Instance";
					break;
				case IE_OTHER: .@retStr$ = "Other Error";
					break;
				case IE_OK: .@retStr$ = "OK"; continue;
					break;
			}
			debugmes "Instance Enter return: " + .@retStr$;
		}
	}
end;
OnTimer1860000:
	instance_announce .ins_id, "Last 1 minute till the instance ends!!", bc_all, 0x00FFFF, FW_BOLD, 20;
end;
OnInstanceEnd:
OnTimer1920000:
	unbindatcmd "joinmvpbosshunt";
	unbindatcmd .joinevent_cmdstr$;
	$TimeEventFlag[.event_flag_index] = 0;
	.ins_id = 0;
	stopnpctimer;
end;
OnJoinEvent:
	if(.mvp_event_status == 0){
		dispbottom "It's not yet the time to join the event", 0x00FFFF;
		end;
	}
	else if(.mvp_event_status == 2){
		//instance_enter("MVP Night Event", -1, -1, getcharid(0), .ins_id);
		dispbottom "Registration for the event has ended", 0x00FFFF;
		end;
	}

	.@num = getarraysize(.join_char_id);
	.@char_id = getcharid(0);
	for(.@i = 0; .@i < .@num; .@i++){
		if(.join_char_id[.@i] == .@char_id){
			dispbottom "You've already joined the MVP Boss Random Summon Event, you will be teleported to the intance map in " 
				+ (120000 - getnpctimer(0)) / 1000 + " seconds...", 0x00FF00;
			end;
		}
	}
	.join_char_id[.@num] = .@char_id;
	dispbottom "You've joined the MVP Boss Random Summon Event, you will be teleported to the intance map in " 
		+ (120000 - getnpctimer(0)) / 1000 + " seconds...", 0x00FF00;
end;
}

1@def01,49,28,4	script	MVPBossRandomiser	-1,8,2,{
OnTouch_:
	if('welcome_flag){
		end;
	}
	'welcome_flag = 1;
	instance_announce 0, "Welcome to MVP Boss Random Summon Event!", 0, 0x00FFFF, FW_BOLD, 20;
end;
OnInit:
	disablenpc;
end;
OnInstanceInit:
	setarray .mvpid[0],
		1086,//	Golden Thief Bug
		1115,//	Eddga
		1115,//	Eddga
		1150,//	Moonlight Flower
		1150,//	Moonlight Flower
		1159,//	Phreeoni
		1159,//	Phreeoni
		1112,//	Drake
		1112,//	Drake
		1583,//	Tao Gunka
		1492,//	Incantation Samurai
		1046,//	Doppelganger
		1046,//	Doppelganger
		1252,//	Garm
		1252,//	Garm
		1418,//	Evil Snake Lord
		1059,//	Mistress
		1190,//	Orc Lord
		1087,//	Orc Hero
		1087,//	Orc Hero
		1251,//	Knight of Windstorm
		1038,//	Osiris
	//	1658,//	Ygnizem
		1272,//	Dark Lord
	//	1871,//	Fallen Bishop
		1039,//	Baphomet
		1147,//	Maya
	//	1785,//	Atroce
		1389,//	Dracula
	//	1630,//	Bacsojin
	//	1885,//	Gorynych
	//	1623,//	RSX 0806
		1511,//	Amon Ra
	//	1688,//	Lady Tanee
	//	1768,//	Gloom Under Night
	//	1719,//	Datale
	//	1734,//	Kiel D-01
		1157,//	Pharaoh
		1373,//	Lord of Death
		1312;//	Turtle General
	//	1779,//	Ktullanux
	//	1874,//	Beelzebub
	//	1646,// Bio3 placeholder
	//	1708,//	Thanatos
	//	1751,//	Valkyrie Randgris
	//	1832;//	Ifrit
	setmapflag strnpcinfo(4), mf_noteleport;
	setmapflag strnpcinfo(4), mf_monster_noteleport;
	callsub SummonRandomMVP;
end;
SummonRandomMVP:
	if(getarraysize(.mvpid) <= 1){
		.@random_mvpid = 0;
	}
	else {
		.@random_mvpid = rand(0, getarraysize(.mvpid)-1);
	}
	if(.mvpid[.@random_mvpid] == 0){
		announce "No More MVP Boss to Summon.", bc_all, 0x00FFFF, FW_BOLD, 16;
		end;
	}
	monster strnpcinfo(4), 50, 50, "--en--", 
			.mvpid[.@random_mvpid], 1, instance_npcname("MVPBossRandomiser") + "::OnMVPQuiled";
	removemapflag strnpcinfo(4), mf_pvp;
	//.@mobGID = $@mobid[0];
	//getunitdata .@mobGID, .@dun_boss_data;
	//.@new_boss_hp = .@dun_boss_data[UMOB_MAXHP] / 5;
	//setunitdata .@mobGID, UMOB_HP, .@new_boss_hp;
	//setunitdata .@mobGID, UMOB_MAXHP, .@new_boss_hp;
	deletearray .mvpid[.@random_mvpid], 1;
end;
OnMVPQuiled:
	getunitdata killedgid, .@mvp_boss_data;
	.@current_map$ = strnpcinfo(4);
	.@player_num = getmapunits(BL_PC, .@current_map$, .@user_id[0]);
	
	for(.@i = 0; .@i < .@player_num; .@i++){
		getmapxy(.@mapName$, .@map_x, .@map_y, BL_PC, .@user_id[.@i]);
		if(distance(.@map_x, .@map_y, .@mvp_boss_data[UMOB_X], .@mvp_boss_data[UMOB_Y]) > 20){
			continue;
		}
		if(rand(1,100) <= 55){
			getitem 984, 1, .@user_id[.@i];
		}
		if(rand(1,100) <= 45){
			getitem 985, 1, .@user_id[.@i];
		}
	}
	if(getarraysize(.mvpid) == 20){
		instance_announce 0, "Congratulations for killing the 10th MVP, receive your reward Brave Warriors", 0, 0x00FFFF, FW_BOLD, 20;
		for(.@i = 0; .@i < .@player_num; .@i++){
			getmapxy(.@mapName$, .@map_x, .@map_y, BL_PC, .@user_id[.@i]);
			if(distance(.@map_x, .@map_y, .@mvp_boss_data[UMOB_X], .@mvp_boss_data[UMOB_Y]) > 20){
				continue;
			}
			getitem 14003, 5, .@user_id[.@i];
		}
	}
	if(getarraysize(.mvpid) == 10){
		instance_announce 0, "Congratulations for killing the 20th MVP, receive your reward Heroic Warriors", 0, 0x00FFFF, FW_BOLD, 20;
		for(.@i = 0; .@i < .@player_num; .@i++){
			getmapxy(.@mapName$, .@map_x, .@map_y, BL_PC, .@user_id[.@i]);
			if(distance(.@map_x, .@map_y, .@mvp_boss_data[UMOB_X], .@mvp_boss_data[UMOB_Y]) > 20){
				continue;
			}
			getitem 7199, 10, .@user_id[.@i];
		}
	}
	if(getarraysize(.mvpid) == 0){
		instance_announce 0, "Congratulations for killing the 30th MVP, receive your reward Legendary Warriors", 0, 0x00FFFF, FW_BOLD, 20;
		for(.@i = 0; .@i < .@player_num; .@i++){
			getmapxy(.@mapName$, .@map_x, .@map_y, BL_PC, .@user_id[.@i]);
			if(distance(.@map_x, .@map_y, .@mvp_boss_data[UMOB_X], .@mvp_boss_data[UMOB_Y]) > 20){
				continue;
			}
			getitem 30007, 2, .@user_id[.@i];
		}
	}
	sleep 5000;
	if(getarraysize(.mvpid) == 0){
		instance_announce 0, "Can't believe it, but it seems you have finished all the available MVP Bosses.", 0, 0x00FFFF, FW_BOLD, 20;
	}
	else {
		instance_announce 0, "The next MVP Boss will be summoned in 5 seconds.", 0, 0x00FFFF, FW_BOLD, 20;
		sleep 5000;
		callsub SummonRandomMVP;
	}
end;
OnInstanceDestroy:
	donpcevent "MVP Boss Random Instance::OnInstanceEnd";
end;
}
