//======= Emperium Arena Instance ============================
//============================================================
-	script	Emperium Arena	-1,{
OnInit:
	bindatcmd("startemparena",strnpcinfo(0)+"::OnInitEvent",99,99);
end;
OnHour22:
OnInitEvent:
	.@test_flag = .@atcmd_parameters$[0];

	if(.ins_id > 0){
		if(playerattached()){
			dispbottom "An instance of Emperium Arena is already active", 0x00FF00;
		}
		else {
			debugmes "An instance of Emperium Arena is already active";
		}
		end;
	}
	if(.@test_flag == 1){
		.target = bc_self;
		attachnpctimer;
	}
	else {
		.target = bc_all;
	}
	
	bindatcmd("joinemparena",strnpcinfo(0)+"::OnJoinEvent",0,99);
	bindatcmd("joinevent",strnpcinfo(0)+"::OnJoinEvent",0,99);
	.ins_id = instance_create("Emperium_Arena", IM_NONE);

	if(.ins_id == -4){
		if(playerattached()){
			dispbottom "No free instance!", 0xFF0000;
		}
		else {
			debugmes "No free instance!";
		}
		end;
	}
	else if(.ins_id < 1){
		if(playerattached()){
			dispbottom "Failed to create instance", 0xFF0000;
		}
		else {
			debugmes "Failed to create instance";
		}
		end;
	}

	deletearray .join_char_id[0], getarraysize(.join_char_id);
	announce "The Emperium Arena will start in 5 minutes, to join the event type @joinemparena or @joinevent", .target, 0x00FFFF;
	.emperium_arena = 1;
	initnpctimer;
end;
OnTimer60000:
	announce "The Emperium Arena will start in 4 minutes, to join the event type @joinemparena or @joinevent", .target, 0x00FFFF;
end;
OnTimer120000:
	announce "The Emperium Arena will start in 3 minutes, to join the event type @joinemparena or @joinevent", .target, 0x00FFFF;
end;
OnTimer180000:
	announce "The Emperium Arena will start in 2 minutes, to join the event type @joinemparena or @joinevent", .target, 0x00FFFF;
end;
OnTimer240000:
	announce "The Emperium Arena will start in 1 minute, to join the event type @joinemparena or @joinevent", .target, 0x00FFFF;
end;
OnTimer300000:
	.emperium_arena = 2;
	donpcevent instance_npcname("Emperium Arena Team A", .ins_id) + "::OnStartEmpArena";
	// Emperium Arena Team A npc will trigger the Emperium Arena Team B OnStartEmpArena event
end;
OnTimer10800000:
OnEmpArenaEnd:
OnTimerQuit:
	unbindatcmd "joinemparena";
	unbindatcmd "joinevent";
	.ins_id = 0;
	.emperium_arena = 0;
	stopnpctimer;
end;
OnJoinEvent:
	if(.emperium_arena == 0){
		dispbottom "It's not yet the time to join the event", 0x00FFFF;
		end;
	}
	else if(.emperium_arena == 2){
		dispbottom "Registration for the event has ended", 0x00FFFF;
		end;
	}

	.@char_id = getcharid(0);
	if(inarray(.join_char_id, .@char_id) == -1){
		.join_char_id[.@num] = .@char_id;
	}
	else {
		message strcharinfo(0), "You've joined the Emperium Arena";
	}

	if(strcharinfo(3) != instance_mapname("06guild_r", .ins_id)){
		.@ret = instance_enter("Emperium_Arena", -1, -1, .@char_id, .ins_id);
		switch(.@ret){
			case IE_NOMEMBER: debugmes "Instance Enter return: No Member";
				break;
			case IE_NOINSTANCE: debugmes "Instance Enter return: No Instance";
				break;
			case IE_OTHER: debugmes "Instance Enter return: Other Error";
				break;
			case IE_OK: // do nothing
				break;
		}
	}
end;
}

06guild_r, 73, 163, 1	script	Emperium Arena Team A	4_M_JOB_KNIGHT1,{
end;
OnInit:
	disablenpc;
end;
OnInstanceInit:
	waitingroom " Join Team A Here", 20, instance_npcname(strnpcinfo(0)) + "::OnCheckUsers", 1;
	setwall strnpcinfo(4), 97, 183, 8,4,0,"empwaitingroowall";
end;
OnInstanceDestroy:
	bg_destroy 'teamA_id;
end;
OnCheckUsers:
	chatmes "Hello! Welcome to Team A!";
	//getwaitingroomusers instance_npcname(strnpcinfo(0));
	//for(.@i = 0; .@i < .@waitingroom_usercount; .@i++){
	//	.@class_id = readparam(Class, .@waitingroom_users[.@i]);
	//	if(eaclass(.@class_id)&EAJ_BASEMASK == EAJ_MAGE || eaclass(.@class_id)&EAJL_2_1){
	//		.@nonempbreaker++;
	//	}
	//}
	//if(.@nonempbreaker == .@waitingroom_usercount - 1){
	//	chatmes "Our team currently has no emperium breakers. Maybe some of you change team?";
	//}
end;
OnStartEmpArena:
	.teamA_count = getwaitingroomstate(0, instance_npcname(strnpcinfo(0)));
	.teamB_count = getwaitingroomstate(0, instance_npcname("Emperium Arena Team B"));

	if( .teamA_count + .teamB_count < 4){
		// give participation reward?
		getwaitingroomusers instance_npcname(strnpcinfo(0));
		for(.@i = 0; .@i < .@waitingroom_usercount; .@i++){
			getitem 7199, 5, .@waitingroom_users[.@i];
		}
		donpcevent instance_npcname("Emperium Arena Team B") + "::OnStartEmpArena";
		end;
	}
	else if((.teamA_count + .teamB_count + 1) / 2 < .teamA_count){
		// kick extra players and add them to team b later
		getwaitingroomusers instance_npcname(strnpcinfo(0));
		.@num = 0;
		for(.@i = ((.teamA_count + .teamB_count + 1) / 2); .@i < .@waitingroom_usercount; .@i++){
			waitingroomkick instance_npcname(strnpcinfo(0)), convertpcinfo(.@waitingroom_users[.@i], CPC_NAME);
			'extra_char_id_b[.@num] = convertpcinfo(.@waitingroom_users[.@i], CPC_CHAR);
			.@num++;
		}
	}
	set 'teamA_id, waitingroom2bg("-", 0, 0, instance_npcname(strnpcinfo(0)) + "::OnArenaQuit", instance_npcname(strnpcinfo(0)) + "::OnArenaDeath");
	for(.@i = 0; .@i < getarraysize('guardianA); .@i++){
		bg_monster_set_team 'guardianA[.@i], 'teamA_id;
		setunitdata 'guardianA[.@i], UMOB_ELETYPE, ELE_GHOST;
		//setunitdata 'guardianA[.@i], UMOB_ELELEVEL, 1;
	}
	//warpwaitingpc instance_mapname("06guild_01"), 8, 50;
	bg_warp 'teamA_id, instance_mapname("06guild_01"), 8, 50;
	donpcevent instance_npcname("Emperium Arena Team B") + "::OnStartEmpArena";
end;
OnArenaDeath:
	showdigit 10, 2;
	sleep2 10000;
	showdigit 0;
	switch(rand(1,3)){
		case 1: warp instance_mapname("06guild_01"), 8, 50;
			break;
		case 2: warp instance_mapname("06guild_01"), 50, 73;
			break;
		case 3: warp instance_mapname("06guild_01"), 34, 64;
			break;
	}
	percentheal 100, 100;
end;
OnArenaQuit:
	percentheal 100, 100;
	if ( !bg_get_data( 'teamA_id, 0 ) )
		mapannounce instance_mapname("06guild_01"), "All Team A members have quit!", bc_map, 0xff3333;
	end;
end;
}

06guild_r, 90, 163, 1	script	Emperium Arena Team B	4_M_JOB_KNIGHT2,{
end;
OnInit:
	disablenpc;
end;
OnInstanceInit:
	waitingroom "Join Team B Here", 20, instance_npcname(strnpcinfo(0)) + "::OnCheckUsers", 1;
end;
OnInstanceDestroy:
	bg_destroy 'teamB_id;
end;
OnCheckUsers:
	chatmes "Hello! Welcome to Team B!";
end;
OnStartEmpArena:
	.@teamA_count = getvariableofnpc(.teamA_count, instance_npcname("Emperium Arena Team A"));
	.@teamB_count = getwaitingroomstate(0, instance_npcname(strnpcinfo(0)));
	
	if( .@teamA_count + .@teamB_count < 4){
		// give participation reward?
		getwaitingroomusers instance_npcname(strnpcinfo(0));
		for(.@i = 0; .@i < .@waitingroom_usercount; .@i++){
			getitem 7199, 5, .@waitingroom_users[.@i];
		}
		.@ins_id = instance_id();
		instance_announce .@ins_id, "Thank you for participating but we have not reached the minimum required pariticpants of 4.", bc_all, 0x00FFFF, FW_BOLD, 20;
		sleep 5000;
		instance_announce .@ins_id, "This Instance Map will be Closed in 30 seconds and you all will be teleported out.", bc_all, 0x00FFFF, FW_BOLD, 20;
		sleep 30000;
		instance_destroy .@ins_id;
		end;
	}
	else if((.@teamA_count + .@teamB_count + 1) / 2 < .@teamB_count){
		// kick extra players and add them to team a later
		getwaitingroomusers instance_npcname(strnpcinfo(0));
		.@num = 0;
		for(.@i = (.@teamA_count + .@teamB_count + 1) / 2; .@i < .@waitingroom_usercount; .@i++){
			waitingroomkick instance_npcname(strnpcinfo(0)), convertpcinfo(.@waitingroom_users[.@i], CPC_NAME);
			'extra_char_id_a[.@num] = convertpcinfo(.@waitingroom_users[.@i], CPC_CHAR);
			.@num++;
		}
	}
	set 'teamB_id, waitingroom2bg("-", 0, 0, instance_npcname(strnpcinfo(0)) + "::OnArenaQuit", instance_npcname(strnpcinfo(0)) + "::OnArenaDeath");
	for(.@i = 0; .@i < getarraysize('guardianB); .@i++){
		bg_monster_set_team 'guardianB[.@i], 'teamB_id;
		setunitdata 'guardianB[.@i], UMOB_ELETYPE, ELE_GHOST;
		//setunitdata 'guardianB[.@i], UMOB_ELELEVEL, 1;
	}
	//warpwaitingpc instance_mapname("06guild_01"), 91, 50;
	bg_warp 'teamB_id, instance_mapname("06guild_01"), 91, 50;
	donpcevent instance_npcname("Emperium Arena NPC") + "::OnCheckExtra";
end;
OnArenaDeath:
	showdigit 10, 2;
	sleep2 10000;
	showdigit 0;
	switch(rand(1,3)){
		case 1: warp instance_mapname("06guild_01"), 91, 50;
			break;
		case 2: warp instance_mapname("06guild_01"), 64, 34;
			break;
		case 3: warp instance_mapname("06guild_01"), 50, 26;
			break;
	}
	percentheal 100, 100;
end;
OnArenaQuit:
	percentheal 100, 100;
	if ( !bg_get_data( 'teamB_id, 0 ) )
		mapannounce instance_mapname("06guild_01"), "All Team B members have quit!", bc_map, 0xff3333;
	end;
end;
}

06guild_01, 50, 50, 3	script	Emperium Arena NPC	-1,{
OnInit:
	disablenpc;
end;
OnInstanceInit:
	.ins_id = instance_id();
	setmapflag strnpcinfo(4), MF_BATTLEGROUND;
	setmapflag strnpcinfo(4), mf_noteleport;
	setmapflag strnpcinfo(4), mf_noloot;
	setmapflag strnpcinfo(4), mf_nopenalty;
	setmapflag strnpcinfo(4), mf_nobranch;
	setmapflag strnpcinfo(4), mf_noicewall;
	setmapflag strnpcinfo(4), mf_hidemobhpbar;
	setmapflag strnpcinfo(4), mf_restricted, 4;
	.brokenTeamAEmp = 0;
	.brokenTeamBEmp = 0;

	'guardianA[0] = bg_monster(1, strnpcinfo(4), 7, 50, "Stone Guardian A 1", 1907, instance_npcname(strnpcinfo(0)) + "::OnTeamAEmpBreak");
	'guardianA[1] = bg_monster(1, strnpcinfo(4), 26, 57, "Stone Guardian A 2", 1907, instance_npcname(strnpcinfo(0)) + "::OnTeamAEmpBreak");
	'guardianA[2] = bg_monster(1, strnpcinfo(4), 26, 42, "Stone Guardian A 3", 1907, instance_npcname(strnpcinfo(0)) + "::OnTeamAEmpBreak");

	'guardianB[0] = bg_monster(1, strnpcinfo(4), 92, 50, "Stone Guardian B 1", 1908, instance_npcname(strnpcinfo(0)) + "::OnTeamBEmpBreak");
	'guardianB[1] = bg_monster(1, strnpcinfo(4), 73, 57, "Stone Guardian B 2", 1908, instance_npcname(strnpcinfo(0)) + "::OnTeamBEmpBreak");
	'guardianB[2] = bg_monster(1, strnpcinfo(4), 73, 42, "Stone Guardian B 3", 1908, instance_npcname(strnpcinfo(0)) + "::OnTeamBEmpBreak");
end;
OnInstanceDestroy:
	donpcevent "Emperium Arena::OnEmpArenaEnd";
end;
OnTeamAEmpBreak:
	if(playerattached() == 0){
		end;
	}
	.brokenTeamAEmp++;
	instance_announce .ins_id, "Team B has broken " + .brokenTeamAEmp + " Emperium", bc_all, 0x00FFFF, FW_BOLD, 20;
	if(.brokenTeamAEmp == 3){
		getitem 7199, 5; // Hourly Coin, extra reward for the last guardian stone breaker
		instance_announce .ins_id, "Team B Won this game!", bc_all, 0x00FFFF, FW_BOLD, 20;
		callsub S_GiveRewards, 'teamB_id;
		doevent instance_npcname(strnpcinfo(0)) + "::OnEmpArenaEnd";
	}
end;
OnTeamBEmpBreak:
	.brokenTeamBEmp++;
	instance_announce .ins_id, "Team A has broken " + .brokenTeamBEmp + " Emperium", bc_all, 0x00FFFF, FW_BOLD, 20;
	if(.brokenTeamBEmp == 3){
		getitem 7199, 5; // Hourly Coin, extra reward for the last guardian stone breaker
		instance_announce .ins_id, "Team A Won this game!", bc_all, 0x00FFFF, FW_BOLD, 20;
		callsub S_GiveRewards, 'teamA_id;
		doevent instance_npcname(strnpcinfo(0)) + "::OnEmpArenaEnd";
	}
end;
S_GiveRewards:
	.@winnerTeam = getarg(0);
	.@player_num = getmapunits(BL_PC, strnpcinfo(4), .@user_id[0]);
	for(.@i = 0; .@i < .@player_num; .@i++){
		if(.@winnerTeam == getcharid(4, convertpcinfo(.@user_id[.@i], CPC_NAME))){
			getitem 7199, 15, .@user_id[.@i];
		}
		else {
			getitem 7199, 5, .@user_id[.@i];
		}
	}
return;
OnEmpArenaEnd:
	killmonster strnpcinfo(4), instance_npcname(strnpcinfo(0)) + "::OnTeamAEmpBreak";
	killmonster strnpcinfo(4), instance_npcname(strnpcinfo(0)) + "::OnTeamBEmpBreak";
	sleep 5000;
	instance_announce .ins_id, "This Instance Map will be Closed in 1 minute and you all will be teleported out.", bc_all, 0x00FFFF, FW_BOLD, 20;
	sleep 60000;
	instance_destroy .ins_id;
end;
OnCheckExtra:
	if(getarraysize('extra_char_id_a) > 0){
		// add to team a
		for(.@i = 0; .@i < getarraysize('extra_char_id_a); .@i++){
			bg_join('teamA_id, instance_mapname("06guild_01"), 8, 50,'extra_char_id_a[.@i]);
		}
	}
	else if(getarraysize('extra_char_id_b) > 0){
		// add to team b
		for(.@i = 0; .@i < getarraysize('extra_char_id_b); .@i++){
			bg_join('teamB_id, instance_mapname("06guild_01"), 91, 50,'extra_char_id_b[.@i]);
		}
	}
end;
}
