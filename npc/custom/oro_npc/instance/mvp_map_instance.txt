//======= MVP Map Instance ===================================
//============================================================
new_3-1,180,86,3	script	MVP Map Instance	4_M_JOB_KNIGHT1,{
	.@npcName$ = "[ MVP Map Instance ]";

	mes .@npcName$;
	mes "Hello, adventurer!";
	mes "If you or your party are ready and is up for some MVP Boss hunting,";
	mes "then you are talking to the right guy!";
	next;

	.@ins_id = instance_id(IM_CHAR);
	if(.@ins_id == 0){
		.@ins_id = instance_id(IM_PARTY);
	}
	if(.@ins_id > 0 && getd("#InsId" + .@ins_id + "flag") == 0){
		mes .@npcName$;
		mes "Your MVP Map instance is now ready.";
		mes "Do you want to enter the instance now?";
		next;

		select("Yes, I want to enter the instance now:Not yet, let me check my inventory again");
		if(@menu == 2){
			mes .@npcName$;
			mes "Talk to me again when you are ready.";
			close;
		}
		
		mes .@npcName$;
		mes "You will now be teleported to the instance dungeon.";
		close2;
		//setd "#InsId" + .@ins_id + "flag", 1;
		instance_enter(instance_live_info(ILI_NAME, .@ins_id), -1, -1, getcharid(0), .@ins_id);
		end;
	}

	mes .@npcName$;
	mes "I can create an intance of the map of the MVP Boss of your choice.";
	mes "I only ask for you to pay me " + mesitemlink(7199) + " 5 pcs. ea or 100,000 Zeny.";
	next;

	mes .@npcName$;
	mes "What you say?";
	mes "This is already a bargain price.";
	next;

	select("Proceed...:Cancel");
	if(@menu == 2){
		mes .@npcName$;
		mes "Oh, well...you can always comeback if you change your mind.";
		close;
	}

	mes .@npcName$;
	mes "Good!";
	mes "Now, select which MVP Boss you want to hunt?";
	next;

	setarray .@mvp_id[1],
		1086,//	Golden Thief Bug
		1115,//	Eddga
		1150,//	Moonlight Flower
		1159,//	Phreeoni
		1112,//	Drake
		1583,//	Tao Gunka
		1492,//	Incantation Samurai
		1046,//	Doppelganger
		1252,//	Garm
		1418,//	Evil Snake Lord
		1059,//	Mistress
		1190,//	Orc Lord
		1087,//	Orc Hero
		1251,//	Knight of Windstorm
		1038,//	Osiris
		1272,//	Dark Lord
		1039,//	Baphomet
		1147,//	Maya
		1389,//	Dracula
		1511,//	Amon Ra
		1157,//	Pharaoh
		1312;//	Turtle General
	
	.@mvp_sel_str$ = "";
	for(.@i = 1; .@i < getarraysize(.@mvp_id); .@i++){
		.@mvp_sel_str$ += strmobinfo(1, .@mvp_id[.@i]) + ":";
	}
	.@mvp_sel_str$ += "Cancel";

	.@sel = select(.@mvp_sel_str$);
	if(.@sel >= getarraysize(.@mvp_id)){
		mes .@npcName$;
		mes "Talk to me again if you have an MVP Boss you want to hunt.";
		close;
	}

	.@ins_mode = IM_PARTY;
	if(getcharid(1) == 0){
		mes .@npcName$;
		mes "It seems that you are not in a party,";
		mes "do you want to proceed in a Solo Instance?";
		next;

		select("Continue as Solo:No, let me call my party");
		if(@menu == 2){
			mes .@npcName$;
			mes "Talk to me again when your party is ready.";
			close;
		}
		.@ins_mode = IM_CHAR;
	}
	else if(is_party_leader() == false){
		mes .@npcName$;
		mes "It seems that you are not the leader of your party,";
		mes "please ask your party leader to talk to me to proceed.";
		close;
	}
	mes .@npcName$;
	mes "You selected the MVP " + strmobinfo(1, .@mvp_id[.@sel]);
	mes "Please select the mode of payment you want to create the instance.";
	next;

	select("5 Hourly Coins:100,000 Zeny");
	if(@menu == 1){
		if(countitem(7199) >= 5){
			mes .@npcName$;
			mes "You selected to pay with " + mesitemlink(7199) + " 5 pcs.";
			mes "Do you want to proceed and confirm your payment?";
			next;

			select("Proceed and confirm:Let me think about it again");
			if(@menu == 2){
				mes .@npcName$;
				mes "Oh, well...you can always comeback if you change your mind.";
				close;
			}
			.@ret = instance_create("MVP " + .@mvp_id[.@sel], .@ins_mode);
			if(.@ret > 0){
				delitem 7199, 5;
				mes .@npcName$;
				mes "Your instance is now ready";
				mes "Do you to enter the instance now?";
				next;

				select("Yes, I want to enter instance now:No, let me prepare my party first");
				if(@menu == 2){
					mes .@npcName$;
					mes "^FF0000Please take note the the instance will be destroyed if there are no players inside for 5 minutes.^000000";
					close;
				}
				mes .@npcName$;
				mes "You will now be teleported to the instance dungeon.";
				close2;
				instance_enter("MVP " + .@mvp_id[.@sel], -1, -1, getcharid(0), .@ret);
				end;
			}
			mes .@npcName$;
			switch(.@ret){
				case -1: mes "Invalid intance type";
					break;
				case -2: mes "Character/Party/Guild/Clan not found";
					break;
				case -3: mes "Instance already exists";
					break;
				case -4: mes "No free instances (MAX_INSTANCE exceeded)";
					break;
			}
			close;
		}
		mes .@npcName$;
		mes "It seems that you don't have enough " + mesitemlink(7199);
		mes "Do you want to proceed and pay zeny instead?";
		next;

		select("No, let me check my storage first:Yes, let me pay with zeny");
		if(@menu == 1){
			mes .@npcName$;
			mes "Talk to me again when you are ready.";
			close;
		}
	}
	if(@menu == 2){
		if(Zeny < 100000){
			mes .@npcName$;
			mes "It seems that you don't have enough zeny.";
			mes "You can talk to me again when you have enough zeny or " + mesitemlink(7199);
			close;
		}
		mes .@npcName$;
		mes "You selected to pay with 100,000 Zeny.";
		mes "Do you want to proceed and confirm your payment?";
		next;

		select("Proceed and confirm:Let me think about it again");
		if(@menu == 2){
			mes .@npcName$;
			mes "Oh, well...you can always comeback if you change your mind.";
			close;
		}
		.@ret = instance_create("MVP " + .@mvp_id[.@sel], .@ins_mode);
		if(.@ret > 0){
			Zeny -= 100000;
			mes .@npcName$;
			mes "Your instance is now ready";
			mes "Do you to enter the instance now?";
			next;

			select("Yes, I want to enter instance now:No, let me prepare my party first");
			if(@menu == 2){
				mes .@npcName$;
				mes "^FF0000Please take note the the instance will be destroyed if there are no players inside for 5 minutes.^000000";
				close;
			}
			mes .@npcName$;
			mes "You will now be teleported to the instance dungeon.";
			close2;
			instance_enter("MVP " + .@mvp_id[.@sel], -1, -1, getcharid(0), .@ret);
			end;
		}
		mes .@npcName$;
		switch(.@ret){
			case -1: mes "Invalid intance type";
				break;
			case -2: mes "Character/Party/Guild/Clan not found";
				break;
			case -3: mes "Instance already exists";
				break;
			case -4: mes "No free instances (MAX_INSTANCE exceeded)";
				break;
		}
		close;
	}
	end;
}

prt_sewb4,100,93,0	script	mvp gtb spawner1	-1,2,2,{
OnInit:
	disablenpc;
end;
OnInstanceInit:
	setmapflag strnpcinfo(4), mf_noteleport;
	setmapflag strnpcinfo(4), mf_monster_noteleport;
	setmapflag strnpcinfo(4), mf_partylock;

//	.@owner_id = instance_live_info(ILI_OWNER);
//	.@ins_mode = instance_live_info(ILI_MODE);

//	if(.@ins_mode == IM_CHAR){
//		.@party_count = 1;
//	}
//	else if(.@ins_mode == IM_PARTY){
//		getpartymember .@owner_id;
//		.@party_count = cap_value($@partymembercount, 1, 10);
//	}
//	else { //normally code doesn't reach here
//		.@party_count = 10;
//	}

	monster strnpcinfo(4), 0, 0, "Instance Golden Bug", 
		"GOLDEN_BUG", 1, instance_npcname("mvp gtb spawner1") + "::OnMVPQuiled";
	'mvp_summon_id = $@mobid[0];

	removemapflag strnpcinfo(4), mf_pvp; //current setting is pvp will be turn on when mvp is spawned, but here we need to turn it off
//	.@mobGID = $@mobid[0];
//	getunitdata .@mobGID, .@dun_boss_data;
//	.@new_boss_hp = .@dun_boss_data[UMOB_MAXHP] / 10;
//	setunitdata .@mobGID, UMOB_HP, .@new_boss_hp * .@party_count;
//	setunitdata .@mobGID, UMOB_MAXHP, .@new_boss_hp * .@party_count;
//	'mvp_mob_x = .@dun_boss_data[UMOB_X];
//	'mvp_mob_y = .@dun_boss_data[UMOB_Y];

	monster strnpcinfo(4), 0, 0, "--en--", "THIEF_BUG_EGG", 10;
	monster strnpcinfo(4), 0, 0, "--en--", "THIEF_BUG", 20;
	monster strnpcinfo(4), 0, 0, "--en--", "THIEF_BUG_", 20;
	monster strnpcinfo(4), 0, 0, "--en--", "THIEF_BUG__", 70;
	monster strnpcinfo(4), 0, 0, "--en--", "DRAINLIAR", 20;
	monster strnpcinfo(4), 0, 0, "--en--", "CRAMP", 5;
end;
OnMVPQuiled:
	if(getcharid(1) == 0){ // party id is 0, of the player who killed the mvp
		getitem 14003, 5; // temporary item reward, will changed to reward box later
	}
	else {
		getpartymember getcharid(1), 2, .@party_mem_acc_id;
		for(.@i = 0; .@i < getarraysize(.@party_mem_acc_id); .@i++){
			getitem 14003, 5, .@party_mem_acc_id[.@i]; // temporary item reward, will changed to reward box later
		}
	}

	sleep 6000;
	instance_announce 0, "The Instance Dungeon will be Closed in 2 minutes and you all will be teleported out.", 0, 0x00FFFF, FW_BOLD, 20;
	sleep 120000;
	instance_destroy;
end;
OnTouch_:
	if('set_mvp_hp_flag == 1){
		end;
	}
	.@owner_id = instance_live_info(ILI_OWNER);
	.@ins_mode = instance_live_info(ILI_MODE);

	if(.@ins_mode == IM_CHAR){
		.@party_count = 1;
	}
	else if(.@ins_mode == IM_PARTY){
		getpartymember .@owner_id;
		.@party_count = cap_value($@partymembercount, 1, 10);
	}
	else { //normally code doesn't reach here
		.@party_count = 10;
	}

	'set_mvp_hp_flag = 1;
	.@mobGID = 'mvp_summon_id; //$@mobid[0];
	getunitdata .@mobGID, .@dun_boss_data;
	.@new_boss_hp = .@dun_boss_data[UMOB_MAXHP] / 10;
	setunitdata .@mobGID, UMOB_HP, .@new_boss_hp * .@party_count;
	setunitdata .@mobGID, UMOB_MAXHP, .@new_boss_hp * .@party_count;
	viewpointmap strnpcinfo(4), 0, .@dun_boss_data[UMOB_X], .@dun_boss_data[UMOB_Y], 1, 0xFF0000;
end;
}
