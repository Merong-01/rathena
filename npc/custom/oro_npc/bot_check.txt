//====== Bot Check Script ====================================
//============================================================
-	script	Bot Check	-1,{
OnHour00:
OnHour03:
OnHour06:
OnHour09:
OnHour12:
OnHour15:
OnHour18:
OnHour21:
	.@count = getunits(BL_PC,.@account_id[0]);
	for(.@i = 0; .@i < .@count; .@i++){
		.@char_id = convertpcinfo(.@account_id[.@i],CPC_CHAR);
		.@char_name$ = convertpcinfo(.@char_id,CPC_NAME);
		if(rand(3) != 1){
			continue;
		}
		
		if(checkidle(.@char_name$) > 60){
			//dispbottom "You are in idle.",0,.@char_id;
			continue;
		}
		
		if(strcharinfo(3,.@char_id) == "sec_pri"){
			//dispbottom "You are in jail.",0,.@char_id;
			continue;
		}
		
		announce "Bot Check will commence in 30 seconds, " + 
			"prepare yourself for a simple logic math problem---Do not logout!!",
			bc_self,0xF55607,FW_NORMAL,14,0,0,.@char_id;
		
		attachrid(.@account_id[.@i]);
		bot_check_flag = 1;
		@bot_check_flag2 = 1;
		showdigit 30,2;
		addtimer 30000,strnpcinfo(0) + "::OnCheckBot";
	}
	end;

OnPCLoginEvent:
	if(playerattached() != 0){
		if(bot_check_flag == 1){
			.@char_id = convertpcinfo(playerattached(),CPC_CHAR);
			showdigit 0;
			setoption 0x40,1,.@char_id;
			ignoretimeout 1;
			callfunc "fx_simple_bot_check", .@char_id,1;
			setoption 0x40,0,.@char_id;
			bot_check_flag = 0;
		}
	}
	end;

OnCheckBot:
	//attachrid(.@account_id[.@i]);
	.@char_id = convertpcinfo(playerattached(),CPC_CHAR);
	showdigit 0;
	ignoretimeout 1,.@char_id;
	if(@bot_check_flag2 == 0){
		detachrid;
		//end;
	}
	else {
		setoption 0x40,1,.@char_id;
		callfunc "fx_simple_bot_check", .@char_id,0;
		bot_check_flag = 0;
		detachrid;
		setoption 0x40,0,.@char_id;
	}
	//end;
}

function	script	fx_simple_bot_check	{
	.@char_id = getarg(0);
	.@tries = getarg(1);
	
	mes "Solve this simple arithmetic:";
	mes "(Tries left: " + (3 - .@tries) + ")";
	.@num1 = rand(6,9);
	.@num2 = rand(1,5);
	switch(rand(1,4)){
		case 1:
			.@cor_answer = .@num1 + .@num2;
			mes "" + .@num1 + " + " + .@num2 + " = ";
			break;
		case 2:
			.@cor_answer = .@num1 - .@num2;
			mes "" + .@num1 + " - " + .@num2 + " = ";
			break;
		case 3:
			.@cor_answer = .@num1 * .@num2;
			mes "" + .@num1 + " x " + .@num2 + " = ";
			break;
		case 4:
		//	.@cor_answer = .@num1 / .@num2;
		//	mes "" + .@num1 + " / " + .@num2 + " = ";
		//	break;
		default:
			.@cor_answer = 2;
			mes "1 + 1 = ";
			break;
	}
	
	input .@answer;
	
	if(.@answer == .@cor_answer){
		mes "I guess you are not a robot...yet";
		close2;
		return;
	}
	else if(.@tries < 2){
		clear;
		mes "Incorrect!! Try again...";
		callfunc "fx_simple_bot_check", .@char_id, .@tries+1;
		return;
	}
	atcommand "@jail " + convertpcinfo(.@char_id,CPC_NAME);
	return;
}
