//===== Customized Quest Gear NPC ============================
//============================================================
-	script	Quest Gear	-1,{
	.@ret = 0;
	
	while(.@ret == 0){
		mes "[Quest Gear Commissioner]";
		mes "Perharps your looking for a new head gear?";
		mes "Why don't you check out my collection...";
		mes "I'm sure you will find what you need.";
		next;
		.@ret = callfunc("F_DisplayMenuType");
	}
	end;
}

function	script	F_DisplayMenuType	{
	set .@sel, select("HeadGear-Upper:HeadGear-Mid:HeadGear-Low:Accessory:Cancel");
	switch(.@sel) {
		case 1: .@ret = callfunc("F_HeadGear_Upper");
			break;
		case 2: .@ret = callfunc("F_HeadGear_Mid");
			break;
		case 3: .@ret = callfunc("F_HeadGear_Low");
			break;
		case 4: .@ret = callfunc("F_Accessory_S");
			break;
		default: mes "[Quest Gear Commissioner]";
				 mes "Talk to me again if you change your mind.";
				 close;
				 .@ret = -1;
			break;
	}
	
	return .@ret;
}

function	script	F_HeadGear_Upper	{
	setarray .@gear_list_id[0], 
		5286,
		5481,
		5388,
		5423,
		5378,
		5208,
		5137; // alice doll
	
	.@gear_list_names$ = "";
	for(.@i = 0; .@i < getarraysize(.@gear_list_id); .@i++){
		.@gear_list_names$ += "^0000CC" + getitemname(.@gear_list_id[.@i]) + "^000000:";
	}
	.@gear_list_names$ += "Cancel";
	
	.@sel = select(.@gear_list_names$);
	switch(.@sel){
		case 1: 
				.@gear_requirements_id[0] = 925;
				.@gear_requirements_count[0] = 3000;
			break;
		case 2: 
				.@gear_requirements_id[0] = 2258;
				.@gear_requirements_count[0] = 1;
				
				.@gear_requirements_id[1] = 2280;
				.@gear_requirements_count[1] = 3;
			break;
		case 3: 
				.@gear_requirements_id[0] = 926;
				.@gear_requirements_count[0] = 3000;
			break;
		case 4: 
				.@gear_requirements_id[0] = 2289;
				.@gear_requirements_count[0] = 3;
				
				.@gear_requirements_id[1] = 2285;
				.@gear_requirements_count[1]= 3;
			break;
		case 5: 
				.@gear_requirements_id[0] = 949;
				.@gear_requirements_count[0] = 3000;
			break;
		case 6: 
				.@gear_requirements_id[0] = 1553;
				.@gear_requirements_count[0] = 4;

				.@gear_requirements_id[1] = 1554;
				.@gear_requirements_count[1] = 4;

				.@gear_requirements_id[2] = 1555;
				.@gear_requirements_count[2] = 2;

				.@gear_requirements_id[3] = 1556;
				.@gear_requirements_count[3] = 2;
			break;
		case 7:
				.@gear_requirements_id[0] = 4208; //miyabi doll card
				.@gear_requirements_count[0] = 1;

				.@gear_requirements_id[1] = 4035; //hydra card
				.@gear_requirements_count[1] = 2;

				.@gear_requirements_id[2] = 5141; //marionette doll hat
				.@gear_requirements_count[2] = 2;

				.@gear_requirements_id[3] = 751; //Osiris doll
				.@gear_requirements_count[3] = 5;

				.@gear_requirements_id[4] = 752; //rocker doll
				.@gear_requirements_count[4] = 5;

				.@gear_requirements_id[5] = 754; //racoon doll
				.@gear_requirements_count[5] = 5;

				.@gear_requirements_id[6] = 743; //spore doll
				.@gear_requirements_count[6] = 5;

				.@gear_requirements_id[7] = 753; //yoyo doll
				.@gear_requirements_count[7] = 5;

				.@gear_requirements_id[8] = 750; //baphomet doll
				.@gear_requirements_count[8] = 1;

				.@gear_requirements_id[9] = 7047; //alice's apron
				.@gear_requirements_count[9] = 150;

				.@gear_requirements_id[10] = 5315; //observer
				.@gear_requirements_count[10] = 1;
			break;
		default: return 0;
			break;
	}
	
	.@isProceed = callfunc("F_DisplayGearRequirements", .@gear_list_id[.@sel-1], 
						.@gear_requirements_id, .@gear_requirements_count);
						
	if(.@isProceed == 1){
		callfunc "F_HandleGearExchange",.@gear_list_id[.@sel-1],
					.@gear_requirements_id,.@gear_requirements_count;
	}
	
	return 0;
}

function	script	F_HeadGear_Mid	{
	setarray .@mid_gear_list_id[0], 5358, 5664, 5800, 5389, 5135;
	
	.@mid_gear_name$ = "";
	for(.@i = 0; .@i < getarraysize(.@mid_gear_list_id); .@i++){
		.@mid_gear_name$ += "^0000CC" + getitemname(.@mid_gear_list_id[.@i]) + "^000000:";
	}
	.@mid_gear_name$ += "Cancel";
	
	.@sel = select(.@mid_gear_name$);
	switch(.@sel){
		case 1: 
				.@gear_requirements_id[0] = 916;
				.@gear_requirements_count[0] = 3000;
			break;
		case 2: 
				.@gear_requirements_id[0] = 2296;
				.@gear_requirements_count[0] = 1;
				
				.@gear_requirements_id[1] = 2274;
				.@gear_requirements_count[1] = 1;
			break;
		case 3: 
				.@gear_requirements_id[0] = 5040;
				.@gear_requirements_count[0] = 3;
			break;
	//	case 4: 
	//			.@gear_requirements_id[0] = 2276;
	//			.@gear_requirements_count[0] = 3;
	//		break;
		case 4: 
				.@gear_requirements_id[0] = 5074;
				.@gear_requirements_count[0] = 1;
				
				.@gear_requirements_id[1] = 5068;
				.@gear_requirements_count[1] = 1;
			break;
		case 5: 
				.@gear_requirements_id[0] = 4010;
				.@gear_requirements_count[0] = 1;
				
				.@gear_requirements_id[1] = 2296;
				.@gear_requirements_count[1] = 1;
			break;
		default: return 0;
			break;
	}
	
	.@isProceed = callfunc("F_DisplayGearRequirements", .@mid_gear_list_id[.@sel-1], 
						.@gear_requirements_id, .@gear_requirements_count);
						
	if(.@isProceed == 1){
		callfunc "F_HandleGearExchange",.@mid_gear_list_id[.@sel-1],
					.@gear_requirements_id,.@gear_requirements_count;
	}
	
	return 0;
}

function	script	F_HeadGear_Low	{
	setarray .@lowGearIdList[0], 5377, 5967;
	
	.@low_gear_name$ = "";
	for(.@i = 0; .@i < getarraysize(.@lowGearIdList); .@i++){
		.@low_gear_name$ += "^0000CC" + getitemname(.@lowGearIdList[.@i]) + "^000000:";
	}
	.@low_gear_name$ += "Cancel";
	
	.@sel = select(.@low_gear_name$);
	switch(.@sel){
		case 1: 
				.@gear_requirements_id[0] = 2268;
				.@gear_requirements_count[0] = 5;
			break;
		case 2:
				.@gear_requirements_id[0] = 5111;
				.@gear_requirements_count[0] = 1;
				
				.@gear_requirements_id[1] = 5358;
				.@gear_requirements_count[1] = 1;
			break;
		default: return 0;
			break;
	}
	
	.@isProceed = callfunc("F_DisplayGearRequirements", .@lowGearIdList[.@sel-1], 
						.@gear_requirements_id, .@gear_requirements_count);
						
	if(.@isProceed == 1){
		callfunc "F_HandleGearExchange",.@lowGearIdList[.@sel-1],
					.@gear_requirements_id,.@gear_requirements_count;
	}
	
	return 0;
}

function	script	F_Accessory_S	{
	setarray .@accessoryIdList[0], 30260; // Preserve Clip
	
	.@accessory_name$ = "";
	for(.@i = 0; .@i < getarraysize(.@accessoryIdList); .@i++){
		.@accessory_name$ += "^0000CC" + getitemname(.@accessoryIdList[.@i]) + "^000000:";
	}
	.@accessory_name$ += "Cancel";
	
	.@sel = select(.@accessory_name$);
	switch(.@sel){
		case 1: 
				.@gear_requirements_id[0] = 2607; // Clip[1]
				.@gear_requirements_count[0] = 2;

				.@gear_requirements_id[1] = 2603; // Necklace[0]
				.@gear_requirements_count[1] = 1;

				.@gear_requirements_id[2] = 2604; // Glove[0]
				.@gear_requirements_count[2] = 1;

				.@gear_requirements_id[3] = 2605; // Brooch[0]
				.@gear_requirements_count[3] = 1;
			break;
		default: return 0;
			break;
	}
	
	.@isProceed = callfunc("F_DisplayGearRequirements", .@accessoryIdList[.@sel-1], 
						.@gear_requirements_id, .@gear_requirements_count);
						
	if(.@isProceed == 1){
		callfunc "F_HandleGearExchange",.@accessoryIdList[.@sel-1],
					.@gear_requirements_id,.@gear_requirements_count;
	}
	
	return 0;
}

function	script	F_DisplayGearRequirements	{
	.@headGear_Id = getarg(0);
	copyarray .@headGearRequirements_Id[0], getarg(1), getarraysize(getarg(1));
	copyarray .@headGearRequirements_Count[0], getarg(2), getarraysize(getarg(2));
	
	mes "[Quest Gear Commissioner]";
	mes "You can have ^0000CC" + getitemname(.@headGear_Id) + "^000000 if you have: ";
	
	for(.@ii = 0; .@ii < getarraysize(.@headGearRequirements_Id); .@ii++){
		.@temp_str$ = "- ^0000CC" + getitemname(.@headGearRequirements_Id[.@ii]) + "^000000";
		.@temp_str$ += " x " + .@headGearRequirements_Count[.@ii];
		if(.@headGearRequirements_Count[.@ii] == 1){
			.@temp_str$ += " pc.";
		}
		else {
			.@temp_str$ += " pcs.";
		}
		mes .@temp_str$;
	}
	next;
	
	return select("Proceed the exchange:Cancel");
}

function	script	F_HandleGearExchange	{
	.@item_id = getarg(0);
	copyarray .@requirements_id[0], getarg(1), getarraysize(getarg(1));
	copyarray .@requirements_count[0], getarg(2), getarraysize(getarg(2));
	.@ret = 0;
	
	for(.@i = 0; .@i < getarraysize(.@requirements_id); .@i++){
		if(countitem(.@requirements_id[.@i]) < .@requirements_count[.@i]){
			.@ret = -1;
			break;
		}
	}
	
	if(.@ret == 0){
		for(.@i = 0; .@i < getarraysize(getarg(1)); .@i++){
			delitem .@requirements_id[.@i],.@requirements_count[.@i];
		}
		getitem .@item_id,1;
		announce "Player [ " + strcharinfo(0) + 
			" ] completed Gear Quest for " + getitemname(.@item_id), 0;
		
		mes "[Quest Gear Commissioner]";
		mes "There you go.";
		mes "Come again if you want another equipment.";
		end;
	}
	else {
		mes "[Quest Gear Commissioner]";
		mes "You don't have the required items.";
		mes "Come back when you have them.";
		end;
	}
}

prontera,147,189,5	duplicate(Quest Gear)	Quest Gear Commissioner#prt	4_M_MERCAT1