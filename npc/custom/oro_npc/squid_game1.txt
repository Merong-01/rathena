//====== Squid Game Parody 1 =================================
//============================================================
//====== Created by Merong of ProjectOvergearedRO ============
//====== Last update 20240812 ================================
-	script	SquidGame1	-1,{
OnInit:
	bindatcmd("startglasstilegame",strnpcinfo(0)+"::OnInitGame",99,99);
end;
//OnClock2130:
//OnHour21:
OnInitGame:
	if(.ins_id > 0){
		if(playerattached()){
			dispbottom "An instance of Emperium Arena is already active", 0x00FF00;
		}
		else {
			debugmes "An instance of Emperium Arena is already active";
		}
		end;
	}
	.test_flag = .@atcmd_parameters$[0];
	if(.test_flag == 1){
		.target = bc_self;
		attachnpctimer;
	}
	else {
		.target = bc_all;
	}
	for(.@i = 0; .@i <= getarraysize($TimeEventFlag); .@i++){
		if($TimeEventFlag[.@i] == 0){
			.event_flag_index = .@i;
			break;
		}
	}
	if(.event_flag_index == 0){
		.joinevent_cmdstr$ = "joinevent";
		$TimeEventFlag[0] = 1;
	}
	else {
		.joinevent_cmdstr$ = "joinevent" + (.event_flag_index + 1);
		$TimeEventFlag[.event_flag_index] = 1;
	}
	bindatcmd("joinglasstilegame", strnpcinfo(0)+"::OnJoinGame",0,99);
	bindatcmd(.joinevent_cmdstr$, strnpcinfo(0)+"::OnJoinGame",0,99);
	.ins_id = instance_create("Glass Tile", IM_NONE);

	if(.ins_id == -4){
		if(playerattached()){
			dispbottom "No free instance!", 0xFF0000;
		}
		else {
			debugmes "No free instance!";
		}
		end;
	}
	else if(.ins_id < 1){
		if(playerattached()){
			dispbottom "Failed to create instance", 0xFF0000;
		}
		else {
			debugmes "Failed to create instance";
		}
		end;
	}

	cleararray .join_char_id[0], 0, getarraysize(.join_char_id);
	announce "The Glass Tile Mini Game will start in 2 minutes, to join the game type @joinglasstilegame or @" + .joinevent_cmdstr$, .target, 0x00FFFF;
	.starting_game = 1;
	initnpctimer;
end;
OnTimer60000:
	announce "The Glass Tile Mini Game will start in 1 minute, to join the game type @joinglasstilegame or @" + .joinevent_cmdstr$, .target, 0x00FFFF;
end;
OnTimer120000:
	.starting_game = 2;
	for(.@i = 0; .@i < getarraysize(.join_char_id); .@i++){
		if(isloggedin(convertpcinfo(.join_char_id[.@i], CPC_ACCOUNT), .join_char_id[.@i])){
			instance_enter("Glass Tile", -1, -1, .join_char_id[.@i], .ins_id);
		}
	}
	announce "A total of " + getarraysize(.join_char_id) + " players have registered to join the game." +
		"The Glass Tile Mini Game is Starting, teleporting participants to the Mini Game instance", .target, 0x00FFFF;
end;
OnTimer725000:
	instance_announce .ins_id, "This Instance Map will be Closed in 30 seconds and you all will be teleported out.", bc_map, 0x00FFFF, FW_NORMAL, 20;
end;
OnTimer755000:
OnTimerQuit:
	instance_destroy .ins_id;
end;
OnInstanceEnd:
	.ins_id = 0;
	unbindatcmd "joinglasstilegame";
	unbindatcmd .joinevent_cmdstr$;
	$TimeEventFlag[.event_flag_index] = 0;
	.starting_game = 0;
	stopnpctimer;
	detachnpctimer;
end;
OnJoinGame:
	if(.starting_game == 0){
		dispbottom "It's not yet the time to join the game", 0xFF0000;
		end;
	}
	else if(.starting_game == 2){
		dispbottom "Registration for the mini game has ended", 0xFF0000;
		end;
	}

	.@num = getarraysize(.join_char_id);
	.@char_id = getcharid(0);
	for(.@i = 0; .@i < .@num; .@i++){
		if(.join_char_id[.@i] == .@char_id){
			dispbottom "You've already joined the Glass Tile mini game, you will be teleported to the intance map in " 
				+ (120000 - getnpctimer(0)) / 1000 + " seconds...", 0x00FF00;
			end;
		}
	}
	.join_char_id[.@num] = .@char_id;
	@glasstile_p = 0;
	@gtiletrapped = 0;
	dispbottom "You've joined the Glass Tile mini game, you will be teleported to the intance map in " 
		+ (120000 - getnpctimer(0)) / 1000 + " seconds...", 0x00FF00;
end;
}

1@tnm1,116,24,3	script	Employee::SGNPC	449,3,4,{
end;
OnTouch:
	if(getgmlevel()){
		end;
	}
	if(@glasstile_p == 0){
		@glasstile_p = 1;
		if(.etouch < 5){
			npctalk "Good job for arriving here safely, here are some hourly coins. But this is only the first level, you can receive more hourly coins at next levels.";
			getitem 7199,3; //Hourly Coin
			.etouch++;
			announce "Congratulations to player [" + strcharinfo(0) + "]  for passing first level and winning 3 "+ getitemname(7199) +"s in today's Glass Tile Mini Game ", 0x0000FF;
		}
		else {
			npctalk "Sorry, But I'm out of Hourly Coins. This is only the first level, you can receive more hourly coins at next levels.";
		}
		donpcevent "::OnSGPillarEffect";
	}
	else if(strnpcinfo(0) == "Team Leader" && @glasstile_p == 1){
		@glasstile_p = 2;
		if(.tltouch < 5){
			npctalk "Impressive! Here are some Horly Coins, There is only the last level left.";
			getitem 7199,4; //Hourly Coin
			.tltouch++;
			announce "Congratulations to player [" + strcharinfo(0) + "]  for passing second level and winning 4 "+ getitemname(7199) +"s in today's Glass Tile Mini Game ", 0x0000FF;
		}
		else {
			npctalk "Sorry, But I'm out of Hourly Coins. There is only the last level left so you better hurry";
		}
		donpcevent "::OnSGPillarEffect";
	}
	else if(strnpcinfo(0) == "Manager" && @glasstile_p == 2){
		@glasstile_p = 3;
		if(.mtouch < 5){
			npctalk "Congratulations! You manage to arrive here safely.";
			getitem 7199,5; //Hourly Coin
			.mtouch++;
			announce "Congratulations to player [" + strcharinfo(0) + "]  for reaching the end and winning 5 "+ getitemname(7199) +"s in today's Glass Tile Mini Game ", bc_all, 0x00FFFF;
			if(.mtouch == 1){
				detachnpctimer "SquidGame1";
				setnpctimer 724999, "SquidGame1";
			}
		}
		else {
			npctalk "Sorry, But I'm out of Hourly Coins. Better luck next time!";
		}
	}
end;
OnInit:
	disablenpc;
end;
OnInstanceDestroy:
	.etouch = 0;
	.tltouch = 0;
	.mtouch = 0;
end;
}

1@tnm1,141,24,3	duplicate(SGNPC)	Team Leader	453,3,4
1@tnm1,157,24,3	duplicate(SGNPC)	Manager	451,2,4

1@tnm1,96,20,0	script	01_gc_01#1::GameCell	139,0,0,{
end;
OnInit:
	disablenpc;
end;
OnInstanceDestroy:
	if(checkwall("wall" + strnpcinfo(3))){
		delwall "wall" + strnpcinfo(3);
	}
end;
OnInstanceInit:
	.@npc_name$ = strnpcinfo(1);
	.@random_num = rand(0,2);
	setd "$@" + strnpcinfo(0) + "flag", 0;
	.@wallName$ = "wall" + strnpcinfo(3);
	.@ret_xy = getmapxy(.@mapname$, .@mapxkor, .@mapykor, BL_NPC, strnpcinfo(3));

	if(.@ret_xy != 0){
		debugmes strnpcinfo(1) + "getmapxy failed!";
	}
	
	if(checkwall(.@wallName$)){
		delwall .@wallName$;
	}
	if(!checkcell(.@mapname$, .@mapxkor, .@mapykor, cell_chknpc)){
		setcell .@mapname$,.@mapxkor,.@mapykor,.@mapxkor,.@mapykor,CELL_NPC,1;
	}

	setd ".tileset"+substr(.@npc_name$,0,1)+"_count", getd(".tileset"+substr(.@npc_name$,0,1)+"_count") +1;
	if(getd(".tileset"+substr(.@npc_name$,0,1)+"_count") == 1){
		setd ".tileset" + substr(.@npc_name$,0,1), 0;
	}
	
	if(.@random_num == 2 && getd(".tileset" + substr(.@npc_name$,0,1)) > 1){
		.@random_num = 1;
	}
	
	if(.@random_num == 1 && getd(".tileset" + substr(.@npc_name$,0,1)) >= 1){
		.@random_num = 0;
	}
	
	if(getd(".tileset"+substr(.@npc_name$,0,1)+"_count") == 7 
		&& getd(".tileset" + substr(.@npc_name$,0,1)) == 0)
	{
		.@random_num = 1;
	}
	
	if(getd(".tileset"+substr(.@npc_name$,0,1)+"_count") == 8 
		&& getd(".tileset" + substr(.@npc_name$,0,1)) == 2)
	{
		.@random_num = 1;
	}
	else if(getd(".tileset"+substr(.@npc_name$,0,1)+"_count") == 8 
		&& getd(".tileset" + substr(.@npc_name$,0,1)) == 1)
	{
		.@random_num = 2;
	}
	
	if(getd(".tileset"+substr(.@npc_name$,0,1)+"_count") == 8){
		setd ".tileset"+substr(.@npc_name$,0,1)+"_count", 0;
	}
	
	if(.@random_num == 2){
		setd ".tileset" + substr(.@npc_name$,0,1), getd(".tileset" + substr(.@npc_name$,0,1)) + 2;
		setd "$@" + strnpcinfo(0) + "flag", 1;
		specialeffect EF_GLASSWALL;
	}
	else if(.@random_num == 1){
		setd ".tileset" + substr(.@npc_name$,0,1), getd(".tileset" + substr(.@npc_name$,0,1)) + 1;
		setd "$@" + strnpcinfo(0) + "flag", 0;
		specialeffect EF_GLASSWALL;
	}
	else {
		setwall .@mapname$, .@mapxkor, .@mapykor, 1, 0, 1, .@wallName$;
		specialeffect EF_BOTTOM_FOGWALL;
		setd "$@" + strnpcinfo(0) + "flag", 2;
	}
end;
OnSGPillarEffect:
	if(getd("$@" + strnpcinfo(0) + "flag") == 1){
		removespecialeffect EF_GLASSWALL;
		specialeffect EF_GLASSWALL;
	}
	else if(getd("$@" + strnpcinfo(0) + "flag") == 2){
		removespecialeffect EF_BOTTOM_FOGWALL;
		specialeffect EF_BOTTOM_FOGWALL;
	}
	else {
		removespecialeffect EF_GLASSWALL;
		specialeffect EF_GLASSWALL;
	}
end;
OnTouch:
	donpcevent "::OnSGPillarEffect";
	if(getgmlevel() > 0 || getgroupid() == 6){
		end;
	}
	
	if(getd("$@" + strnpcinfo(0) + "flag") == 1){
		mapannounce strnpcinfo(4), strcharinfo(0) + ", you stepped on a trap. You're out!", bc_map;
		percentheal -100,0; //participant eliminated
		specialeffect2 EF_CRITICALWOUND;
	}
	else {
		specialeffect EF_HAPGYEOK;
	}
end;
}

1@tnm1,96,21,0	duplicate(GameCell)	01_gc_02#2	139,0,0
1@tnm1,96,22,0	duplicate(GameCell)	01_gc_03#3	139,0,0
1@tnm1,96,23,0	duplicate(GameCell)	01_gc_04#4	139,0,0
1@tnm1,96,24,0	duplicate(GameCell)	01_gc_05#5	139,0,0
1@tnm1,96,25,0	duplicate(GameCell)	01_gc_06#6	139,0,0
1@tnm1,96,26,0	duplicate(GameCell)	01_gc_07#7	139,0,0
1@tnm1,96,27,0	duplicate(GameCell)	01_gc_08#8	139,0,0

1@tnm1,99,20,0	duplicate(GameCell)	02_gc_01#1	139,0,0
1@tnm1,99,21,0	duplicate(GameCell)	02_gc_02#2	139,0,0
1@tnm1,99,22,0	duplicate(GameCell)	02_gc_03#3	139,0,0
1@tnm1,99,23,0	duplicate(GameCell)	02_gc_04#4	139,0,0
1@tnm1,99,24,0	duplicate(GameCell)	02_gc_05#5	139,0,0
1@tnm1,99,25,0	duplicate(GameCell)	02_gc_06#6	139,0,0
1@tnm1,99,26,0	duplicate(GameCell)	02_gc_07#7	139,0,0
1@tnm1,99,27,0	duplicate(GameCell)	02_gc_08#8	139,0,0

1@tnm1,102,20,0	duplicate(GameCell)	03_gc_01#1	139,0,0
1@tnm1,102,21,0	duplicate(GameCell)	03_gc_02#2	139,0,0
1@tnm1,102,22,0	duplicate(GameCell)	03_gc_03#3	139,0,0
1@tnm1,102,23,0	duplicate(GameCell)	03_gc_04#4	139,0,0
1@tnm1,102,24,0	duplicate(GameCell)	03_gc_05#5	139,0,0
1@tnm1,102,25,0	duplicate(GameCell)	03_gc_06#6	139,0,0
1@tnm1,102,26,0	duplicate(GameCell)	03_gc_07#7	139,0,0
1@tnm1,102,27,0	duplicate(GameCell)	03_gc_08#8	139,0,0

1@tnm1,105,20,0	duplicate(GameCell)	04_gc_01#1	139,0,0
1@tnm1,105,21,0	duplicate(GameCell)	04_gc_02#2	139,0,0
1@tnm1,105,22,0	duplicate(GameCell)	04_gc_03#3	139,0,0
1@tnm1,105,23,0	duplicate(GameCell)	04_gc_04#4	139,0,0
1@tnm1,105,24,0	duplicate(GameCell)	04_gc_05#5	139,0,0
1@tnm1,105,25,0	duplicate(GameCell)	04_gc_06#6	139,0,0
1@tnm1,105,26,0	duplicate(GameCell)	04_gc_07#7	139,0,0
1@tnm1,105,27,0	duplicate(GameCell)	04_gc_08#8	139,0,0

1@tnm1,108,20,0	duplicate(GameCell)	05_gc_01#1	139,0,0
1@tnm1,108,21,0	duplicate(GameCell)	05_gc_02#2	139,0,0
1@tnm1,108,22,0	duplicate(GameCell)	05_gc_03#3	139,0,0
1@tnm1,108,23,0	duplicate(GameCell)	05_gc_04#4	139,0,0
1@tnm1,108,24,0	duplicate(GameCell)	05_gc_05#5	139,0,0
1@tnm1,108,25,0	duplicate(GameCell)	05_gc_06#6	139,0,0
1@tnm1,108,26,0	duplicate(GameCell)	05_gc_07#7	139,0,0
1@tnm1,108,27,0	duplicate(GameCell)	05_gc_08#8	139,0,0

1@tnm1,111,20,0	duplicate(GameCell)	06_gc_01#1	139,0,0
1@tnm1,111,21,0	duplicate(GameCell)	06_gc_02#2	139,0,0
1@tnm1,111,22,0	duplicate(GameCell)	06_gc_03#3	139,0,0
1@tnm1,111,23,0	duplicate(GameCell)	06_gc_04#4	139,0,0
1@tnm1,111,24,0	duplicate(GameCell)	06_gc_05#5	139,0,0
1@tnm1,111,25,0	duplicate(GameCell)	06_gc_06#6	139,0,0
1@tnm1,111,26,0	duplicate(GameCell)	06_gc_07#7	139,0,0
1@tnm1,111,27,0	duplicate(GameCell)	06_gc_08#8	139,0,0

1@tnm1,121,20,0	duplicate(GameCell)	07_gc_01#1	139,0,0
1@tnm1,121,21,0	duplicate(GameCell)	07_gc_02#2	139,0,0
1@tnm1,121,22,0	duplicate(GameCell)	07_gc_03#3	139,0,0
1@tnm1,121,23,0	duplicate(GameCell)	07_gc_04#4	139,0,0
1@tnm1,121,24,0	duplicate(GameCell)	07_gc_05#5	139,0,0
1@tnm1,121,25,0	duplicate(GameCell)	07_gc_06#6	139,0,0
1@tnm1,121,26,0	duplicate(GameCell)	07_gc_07#7	139,0,0
1@tnm1,121,27,0	duplicate(GameCell)	07_gc_08#8	139,0,0

1@tnm1,124,20,0	duplicate(GameCell)	08_gc_01#1	139,0,0
1@tnm1,124,21,0	duplicate(GameCell)	08_gc_02#2	139,0,0
1@tnm1,124,22,0	duplicate(GameCell)	08_gc_03#3	139,0,0
1@tnm1,124,23,0	duplicate(GameCell)	08_gc_04#4	139,0,0
1@tnm1,124,24,0	duplicate(GameCell)	08_gc_05#5	139,0,0
1@tnm1,124,25,0	duplicate(GameCell)	08_gc_06#6	139,0,0
1@tnm1,124,26,0	duplicate(GameCell)	08_gc_07#7	139,0,0
1@tnm1,124,27,0	duplicate(GameCell)	08_gc_08#8	139,0,0

1@tnm1,127,20,0	duplicate(GameCell)	09_gc_01#1	139,0,0
1@tnm1,127,21,0	duplicate(GameCell)	09_gc_02#2	139,0,0
1@tnm1,127,22,0	duplicate(GameCell)	09_gc_03#3	139,0,0
1@tnm1,127,23,0	duplicate(GameCell)	09_gc_04#4	139,0,0
1@tnm1,127,24,0	duplicate(GameCell)	09_gc_05#5	139,0,0
1@tnm1,127,25,0	duplicate(GameCell)	09_gc_06#6	139,0,0
1@tnm1,127,26,0	duplicate(GameCell)	09_gc_07#7	139,0,0
1@tnm1,127,27,0	duplicate(GameCell)	09_gc_08#8	139,0,0

1@tnm1,130,20,0	duplicate(GameCell)	10_gc_01#1	139,0,0
1@tnm1,130,21,0	duplicate(GameCell)	10_gc_02#2	139,0,0
1@tnm1,130,22,0	duplicate(GameCell)	10_gc_03#3	139,0,0
1@tnm1,130,23,0	duplicate(GameCell)	10_gc_04#4	139,0,0
1@tnm1,130,24,0	duplicate(GameCell)	10_gc_05#5	139,0,0
1@tnm1,130,25,0	duplicate(GameCell)	10_gc_06#6	139,0,0
1@tnm1,130,26,0	duplicate(GameCell)	10_gc_07#7	139,0,0
1@tnm1,130,27,0	duplicate(GameCell)	10_gc_08#8	139,0,0

1@tnm1,133,20,0	duplicate(GameCell)	11_gc_01#1	139,0,0
1@tnm1,133,21,0	duplicate(GameCell)	11_gc_02#2	139,0,0
1@tnm1,133,22,0	duplicate(GameCell)	11_gc_03#3	139,0,0
1@tnm1,133,23,0	duplicate(GameCell)	11_gc_04#4	139,0,0
1@tnm1,133,24,0	duplicate(GameCell)	11_gc_05#5	139,0,0
1@tnm1,133,25,0	duplicate(GameCell)	11_gc_06#6	139,0,0
1@tnm1,133,26,0	duplicate(GameCell)	11_gc_07#7	139,0,0
1@tnm1,133,27,0	duplicate(GameCell)	11_gc_08#8	139,0,0

1@tnm1,136,20,0	duplicate(GameCell)	12_gc_01#1	139,0,0
1@tnm1,136,21,0	duplicate(GameCell)	12_gc_02#2	139,0,0
1@tnm1,136,22,0	duplicate(GameCell)	12_gc_03#3	139,0,0
1@tnm1,136,23,0	duplicate(GameCell)	12_gc_04#4	139,0,0
1@tnm1,136,24,0	duplicate(GameCell)	12_gc_05#5	139,0,0
1@tnm1,136,25,0	duplicate(GameCell)	12_gc_06#6	139,0,0
1@tnm1,136,26,0	duplicate(GameCell)	12_gc_07#7	139,0,0
1@tnm1,136,27,0	duplicate(GameCell)	12_gc_08#8	139,0,0

1@tnm1,144,20,0	duplicate(GameCell)	13_gc_01#1	139,0,0
1@tnm1,144,21,0	duplicate(GameCell)	13_gc_02#2	139,0,0
1@tnm1,144,22,0	duplicate(GameCell)	13_gc_03#3	139,0,0
1@tnm1,144,23,0	duplicate(GameCell)	13_gc_04#4	139,0,0
1@tnm1,144,24,0	duplicate(GameCell)	13_gc_05#5	139,0,0
1@tnm1,144,25,0	duplicate(GameCell)	13_gc_06#6	139,0,0
1@tnm1,144,26,0	duplicate(GameCell)	13_gc_07#7	139,0,0
1@tnm1,144,27,0	duplicate(GameCell)	13_gc_08#8	139,0,0

1@tnm1,147,20,0	duplicate(GameCell)	14_gc_01#1	139,0,0
1@tnm1,147,21,0	duplicate(GameCell)	14_gc_02#2	139,0,0
1@tnm1,147,22,0	duplicate(GameCell)	14_gc_03#3	139,0,0
1@tnm1,147,23,0	duplicate(GameCell)	14_gc_04#4	139,0,0
1@tnm1,147,24,0	duplicate(GameCell)	14_gc_05#5	139,0,0
1@tnm1,147,25,0	duplicate(GameCell)	14_gc_06#6	139,0,0
1@tnm1,147,26,0	duplicate(GameCell)	14_gc_07#7	139,0,0
1@tnm1,147,27,0	duplicate(GameCell)	14_gc_08#8	139,0,0

1@tnm1,150,20,0	duplicate(GameCell)	15_gc_01#1	139,0,0
1@tnm1,150,21,0	duplicate(GameCell)	15_gc_02#2	139,0,0
1@tnm1,150,22,0	duplicate(GameCell)	15_gc_03#3	139,0,0
1@tnm1,150,23,0	duplicate(GameCell)	15_gc_04#4	139,0,0
1@tnm1,150,24,0	duplicate(GameCell)	15_gc_05#5	139,0,0
1@tnm1,150,25,0	duplicate(GameCell)	15_gc_06#6	139,0,0
1@tnm1,150,26,0	duplicate(GameCell)	15_gc_07#7	139,0,0
1@tnm1,150,27,0	duplicate(GameCell)	15_gc_08#8	139,0,0

1@tnm1,153,20,0	duplicate(GameCell)	16_gc_01#1	139,0,0
1@tnm1,153,21,0	duplicate(GameCell)	16_gc_02#2	139,0,0
1@tnm1,153,22,0	duplicate(GameCell)	16_gc_03#3	139,0,0
1@tnm1,153,23,0	duplicate(GameCell)	16_gc_04#4	139,0,0
1@tnm1,153,24,0	duplicate(GameCell)	16_gc_05#5	139,0,0
1@tnm1,153,25,0	duplicate(GameCell)	16_gc_06#6	139,0,0
1@tnm1,153,26,0	duplicate(GameCell)	16_gc_07#7	139,0,0
1@tnm1,153,27,0	duplicate(GameCell)	16_gc_08#8	139,0,0


1@tnm1,90,20,0	script	Squid Game Controller	-1,{
OnInit:
	disablenpc;
end;
OnInstanceInit:
	setmapflag strnpcinfo(4), mf_noskill;
	setmapflag strnpcinfo(4), mf_noitemconsumption;
	initnpctimer;
end;
OnInstanceDestroy:
	stopnpctimer;
	donpcevent "SquidGame1::OnInstanceEnd";
end;
OnTimer122000:
	donpcevent "::OnSGPillarEffect";
end;
OnTimer123000:
	instance_announce 0, "Reach the other end of the map within 10 minutes, the first 5 players to pass each level will receive 3/4/5" + getitemname(7199), 0;
end;
OnTimer213000:
	instance_announce 0, "1 minute and 30 seconds has passed, summoning monsters to encourage everyone", 0;
	switch(rand(1,2)){
		case 1: monster strnpcinfo(4), 92, 24, "Hunter Fly", 1422, 5;
			break;
		case 2: monster strnpcinfo(4), 92, 24, "Gas Cloud", "NOXIOUS", 5;
			break;
	}
end;
OnTimer373000:
	instance_announce 0, "4 minutes and 10 seconds has passed, summoning more monsters to encourage everyone", 0;
	switch(rand(1,2)){
		case 1: monster strnpcinfo(4), 116, 24, "Hunter Fly", 1422, 5;
			break;
		case 2: monster strnpcinfo(4), 116, 24, "Gas Cloud", "NOXIOUS", 5;
			break;
	}
end;
OnTimer523000:
	instance_announce 0, "6 minutes and 40 seconds has passed, summoning more monsters to encourage everyone", 0;
	switch(rand(1,2)){
		case 1: monster strnpcinfo(4), 141, 24, "Hunter Fly", 1422, 5;
			break;
		case 2: monster strnpcinfo(4), 141, 24, "Gas Cloud", "NOXIOUS", 5;
			break;
	}
end;
OnTimer725000:
	instance_announce 0, "10 minutes has passed, the Game is Over!", 0;
	//instance_warpall "SavePoint", 0, 0;
end;
}
